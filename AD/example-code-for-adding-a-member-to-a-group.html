<!DOCTYPE html>
<html lang="en">
<head>
<meta charset="utf-8">
<title>Example Code for Adding a Member to a Group</title>
<style>
table th { border: 1px solid; }
table td { border: 1px solid; }
</style>
</head>
<body>
<hr />
<p>title: Example Code for Adding a Member to a Group
description: This topic contains code examples that add a member to a group.
ms.assetid: 306fcadb-a492-41e5-bfb3-8beaaec24fb5
ms.tgt_platform: multiple
keywords:</p>
<ul>
<li>Active Directory examples Active Directory , adding a member to a group
ms.topic: article
ms.date: 05/31/2018</li>
</ul>
<hr />
<h1>Example Code for Adding a Member to a Group</h1>
<p>This topic contains code examples that add a member to a group. The Visual Basic Scripting Edition (VBScript) and C++ examples add a member by adding the <a href="/windows/desktop/api/iads/nn-iads-iads"><strong>IADs</strong></a> object that represents the member to the <a href="/windows/desktop/api/iads/nn-iads-iadsgroup"><strong>IADsGroup</strong></a> object that represents the group. The Visual Basic .NET and C# examples modify the member property of the <a href="/dotnet/api/system.directoryservices.directoryentry">DirectoryEntry</a> object that represents the group.</p>
<p>The following C# code examples add an existing member to a group. The function takes the ADsPath of the group container and the distinguished name of the member to be added to the group. The ADsPath is used to create a <a href="/dotnet/api/system.directoryservices.directoryentry">DirectoryEntry</a> object that represents the group. The <a href="/dotnet/api/system.directoryservices.propertyvaluecollection.add#System_DirectoryServices_PropertyValueCollection_Add_System_Object_">PropertyValueCollection.Add</a> method adds to the group the member whose distinguished name was passed to the function. The function then uses the <a href="/dotnet/api/system.directoryservices.directoryentry.commitchanges#System_DirectoryServices_DirectoryEntry_CommitChanges">DirectoryEntry.CommitChanges</a> method to write the new member information to the database.</p>
<p>Call the function with the following parameters:</p>
<ul>
<li><em>bindString</em>: a valid ADsPath for a group container, such as &quot;LDAP://fabrikam.com/CN=TestGroup,OU=TestOU,DC=fabrikam,DC=com&quot;</li>
<li><em>newMember</em>: the distinguished name of the member to be added to the group, such as &quot;CN=JeffSmith,OU=TestOU,DC=fabrikam,DC=com&quot;</li>
</ul>
<pre lang="CSharp"><code>private void AddMemberToGroup(

                                string bindString,

                                string newMember

                                )

{

    try
    {

        DirectoryEntry ent = new DirectoryEntry( bindString );

        ent.Properties[&quot;member&quot;].Add(newMember);

        ent.CommitChanges();

    }

    catch (Exception e)
    {

        Console.WriteLine( &quot;An error occurred.&quot;);

        Console.WriteLine( &quot;{0}&quot;, e.Message);

        return;

    }

}
</code></pre>
<p>The following Visual Basic .NET code examples add an existing member to a group. The function takes the ADsPath of the group container and the distinguished name of the member to be added to the group. The ADsPath is used to create a <a href="/dotnet/api/system.directoryservices.directoryentry">DirectoryEntry</a> object that represents the group. The <a href="/dotnet/api/system.directoryservices.propertyvaluecollection.add#System_DirectoryServices_PropertyValueCollection_Add_System_Object_">PropertyValueCollection.Add</a> method adds to the group the member whose distinguished name was passed to the function. The function then uses the <a href="/dotnet/api/system.directoryservices.directoryentry.commitchanges#System_DirectoryServices_DirectoryEntry_CommitChanges">DirectoryEntry.CommitChanges</a> method to write the new member information to the database.</p>
<p>Call the function with the following parameters:</p>
<ul>
<li><em>bindString</em>: a valid ADsPath for a group container, such as &quot;LDAP://fabrikam.com/CN=TestGroup,OU=TestOU,DC=fabrikam,DC=com&quot;</li>
<li><em>newMember</em>: the distinguished name of the member to be added to the group, such as &quot;CN=JeffSmith,OU=TestOU,DC=fabrikam,DC=com&quot;</li>
</ul>
<pre lang="VB"><code>Private Sub AddMemberToGroup(ByVal bindString As String, 
                                      ByVal newMember As String)

    Try

        Dim ent As New DirectoryEntry(bindString)

        ent.Properties(&quot;member&quot;).Add(newMember)

        ent.CommitChanges()

    Catch e As Exception

        Console.WriteLine(&quot;An error occurred.&quot;)

        Console.WriteLine(&quot;{0}&quot;, e.Message)

        Return

    End Try

End Sub
</code></pre>
<p>The following VBScript example adds an existing member to a group. The script adds the user, Jeff Smith, to the TestGroup group.</p>
<pre lang="VB"><code>Option Explicit

On Error Resume Next

Dim scriptResult        ' Script success or failure

Dim groupPath           ' ADsPath to the group container

Dim group               ' Group object

Dim memberPath          ' ADsPath to the member

Dim member              ' Member object

Dim groupMemberList     ' Used to display group members

Dim errorText           ' Error handing text

scriptResult = False

groupPath = &quot;LDAP://fabrikam.com/CN=TestGroup,OU=TestOU,DC=fabrikam,DC=com&quot;

memberPath = &quot;LDAP://CN=JeffSmith,OU=TestOU,DC=fabrikam,DC=com&quot;

WScript.Echo(&quot;Retrieving group object&quot;)

Set group = GetObject(groupPath)

If Err.number &lt;&gt; vbEmpty then

    Call ErrorHandler(&quot;Could not create group object.&quot;)

End If

Call ShowMembers(groupPath)     'Optional function call

WScript.Echo(&quot;Retrieving new member object&quot;)

Set member = GetObject(memberPath)

If Err.number &lt;&gt; vbEmpty then

    Call ErrorHandler(&quot;Could not get new member object.&quot;)

End If

WScript.Echo(&quot;Adding member to group.&quot;)

group.Add(member.ADsPath)

If Err.number &lt;&gt; vbEmpty then

    Call ErrorHandler(&quot;Could not add member to group.&quot;)

End If

Call ShowMembers(groupPath)     ' Optional function call

scriptResult = True

Call FinalResult(scriptResult)

'****************************************************************
' This function displays the members of a group. The function
' takes the ADsPath of the group.
'****************************************************************

Sub ShowMembers(groupPath)

    Dim groupMember

    Dim groupMemberList

    Dim groupObject

    Set groupObject = GetObject(groupPath)

    Set groupMemberList = groupObject.Members

    Select Case groupMemberList.Count

        Case 1 

            WScript.Echo vbcrlf &amp; &quot;The group has one member.&quot;

        Case 0

            WScript.Echo vbcrlf &amp; &quot;The group has no members.&quot;

        Case Else

            WScript.Echo vbcrlf &amp; &quot;The group has &quot; &amp; groupMemberList.Count &amp; &quot; members.&quot;

    End Select

    If groupMemberList.Count &gt; 0 then

        WScript.Echo vbcrlf &amp; &quot;Here is a member list.&quot;

        For Each groupMember in groupMemberList

            WScript.Echo groupMember.Name

        Next

        WScript.Echo vbcrlf

    End If

    Set groupObject = Nothing

    Set groupMemberList = Nothing

End Sub

'****************************************************************
' This function shows if the script succeeded or failed. The 
' function processed the scriptResult variable.
'****************************************************************

Sub FinalResult(scriptResult)

    WScript.Echo vbcrlf

    If scriptResult = False then

        WScript.Echo &quot;Script failed.&quot;

    Else

        WScript.Echo(&quot;Script successfully completed.&quot;)

    End If

    WScript.Quit

End Sub

'****************************************************************
' This function handles errors that occur in the script.
'****************************************************************

Sub ErrorHandler( errorText )

    WScript.Echo(vbcrlf &amp; errorText)

    WScript.Echo(&quot;Error number: &quot; &amp; Err.number)

    WScript.Echo(&quot;Error Description: &quot; &amp; Err.Description)

    Err.Clear 

    Call FinalResult(scriptResult)

End Sub

</code></pre>
<p>The following C++ code example adds an existing member to a group.</p>
<pre lang="C++"><code>/////////////////////////////////////////////////////////////
/*  AddMemberToGroup() - Adds the passed directory object as a member 
                         of passed group
 
    Parameters
 
        IADsGroup * pGroup   - Group to hold the new IDirectoryObject.
        IADs* pIADsNewMember - Object which will become a member 
                               of the group. Object can be a user, 
                               contact, or group.
*/
HRESULT AddMemberToGroup(IADsGroup * pGroup, IADs* pIADsNewMember)
{
    HRESULT hr = E_INVALIDARG;
    if ((!pGroup)||(!pIADsNewMember))
        return hr;
 
    // Use the IADs::get_ADsPath() member to get the ADsPath.
    // When the ADsPath string is returned, to add the new
    // member to the group, call the IADsGroup::Add() member,
    // passing in the ADsPath string.
    // Query the new member for its AdsPath.
    // This is a fully qualified LDAP path to the object to add.
    BSTR bsNewMemberPath;
    hr = pIADsNewMember-&gt;get_ADsPath(&amp;bsNewMemberPath); 
    if (SUCCEEDED(hr))
    {
 
        // Use the IADsGroup interface to add the new member.
        // Pass the LDAP path to the 
        // new member to the IADsGroup::Add() member
        hr = pGroup-&gt;Add(bsNewMemberPath);
 
        // Free the string returned from IADs::get_ADsPath()
        SysFreeString(bsNewMemberPath);
    }
    return hr;
}
</code></pre>
<h2>Related topics</h2>
<!-- raw HTML omitted -->
<p><a href="adding-members-to-groups-in-a-domain.html">Adding Members to Groups in a Domain</a></p>
<!-- raw HTML omitted -->
<p><a href="/dotnet/api/system.directoryservices.directoryentry">DirectoryEntry</a></p>
<!-- raw HTML omitted -->
<p><a href="/dotnet/api/system.directoryservices.directoryentry.commitchanges#System_DirectoryServices_DirectoryEntry_CommitChanges">DirectoryEntry.CommitChanges</a></p>
<!-- raw HTML omitted -->
<p><a href="/windows/desktop/api/iads/nn-iads-iads"><strong>IADs</strong></a></p>
<!-- raw HTML omitted -->
<p><a href="/windows/desktop/api/iads/nn-iads-iadsgroup"><strong>IADsGroup</strong></a></p>
<!-- raw HTML omitted -->
<p><a href="/dotnet/api/system.directoryservices.propertyvaluecollection.add#System_DirectoryServices_PropertyValueCollection_Add_System_Object_">PropertyValueCollection.Add</a></p>
<!-- raw HTML omitted -->
<p>Â </p>
<p>Â </p>
</body>
