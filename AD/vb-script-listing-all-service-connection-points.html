<!DOCTYPE html>
<html lang="en">
<head>
<meta charset="utf-8">
<title>VB Script Listing all Service Connection Points</title>
<style>
table th { border: 1px solid; }
table td { border: 1px solid; }
</style>
</head>
<body>
<hr />
<p>title: VB Script Listing all Service Connection Points
ms.assetid: 7a76f872-3e4e-4bb7-8f2d-e30b1246369f
ms.tgt_platform: multiple
description: &quot;Learn more about: VB Script Listing all Service Connection Points&quot;
ms.topic: article
ms.date: 05/31/2018
topic_type:</p>
<ul>
<li>kbArticle
api_name:
api_type:
api_location:</li>
</ul>
<hr />
<h1>VB Script Listing all Service Connection Points</h1>
<p>Service Connection Points can be managed using VB Script. The following sample code shows how to list all Service Connection Points on the domain name provided on the command line:</p>
<pre lang="VB"><code>Option Explicit

Dim scpDN
scpDN = WScript.Arguments.Named(&quot;SCP&quot;)

If ( scpDN = &quot;&quot; ) Then
    Usage
Else
    ScpProperties scpDN
End If

' End of Main

Sub Usage
    WScript.Echo &quot;USAGE:  ScpProperties /SCP:&lt;SCP DN&gt;&quot;
    WScript.Echo &quot;&quot;
    Wscript.Echo &quot;Remember to put the SCP DN between quotes&quot;
End Sub

Sub ScpProperties( strDN )

    Dim oConn ' As ADODB.Connection
    Set oConn = CreateObject(&quot;ADODB.Connection&quot;)
    
    oConn.Open &quot;Provider=ADsDSOObject&quot;
    
    Dim oCmd ' As ADODB.Command
    Set oCmd = CreateObject(&quot;ADODB.Command&quot;)
    oCmd.ActiveConnection = oConn
    
    ' In the next command, the real magic is in knowing LDAP queries
    ' GC means to ask the global catalog.
    oCmd.CommandText = &quot;&lt;GC://&quot; &amp; strDN &amp; &quot;&gt;;&quot; &amp; _
                        &quot;(objectClass=ServiceConnectionPoint);&quot; &amp; _
                        &quot;distinguishedname,name,serviceDNSName,&quot; &amp; _
                        &quot;serviceDNSNameType,serviceBindingInformation,&quot; &amp; _
                        &quot;serviceClassName,Keywords;&quot; &amp; _
                        &quot;subtree&quot;

    Dim oRs ' As ADODB.Recordset
    Set oRs = oCmd.Execute
    
    Dim lCount ' As Long
    lCount = 0
    Do While (Not oRs.EOF)
    
        Wscript.Echo &quot;SCP Name: &quot; &amp; oRs(&quot;name&quot;)
        Wscript.Echo &quot;DN: &quot; &amp; oRs(&quot;distinguishedname&quot;)
        Wscript.Echo &quot;serviceDNSName: &quot; &amp; oRs(&quot;serviceDNSName&quot;)
        Wscript.Echo &quot;serviceDNSNameType: &quot; &amp; oRs(&quot;serviceDNSNameType&quot;)
        Wscript.Echo &quot;serviceClassName: &quot; &amp; oRs(&quot;serviceClassName&quot;)
        Dim a ' As Variant
        Dim l 'As Long
        
        a = oRs(&quot;serviceBindingInformation&quot;)
        
        If (Not IsNull(a)) Then
            Wscript.Echo &quot;serviceBindingInformation:&quot;
            For l = LBound(a) To UBound(a)
                Wscript.Echo vbTab &amp; a(l)
            Next
            Wscript.Echo 
        End If
        
        a = oRs(&quot;Keywords&quot;)
        If (Not IsNull(a)) Then
            Wscript.Echo &quot;Keywords: &quot;
            For l = LBound(a) To UBound(a)
                Wscript.Echo vbTab &amp; a(l)
            Next
        End If
        
        oRs.MoveNext
        lCount = lCount + 1
        Wscript.Echo 
    Loop
    
    Wscript.Echo &quot;Total objects = &quot; &amp; lCount
    oConn.Close
    
End Sub
    
</code></pre>
<p>Â </p>
<p>Â </p>
</body>
