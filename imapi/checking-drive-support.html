<!DOCTYPE html>
<html lang="en">
<head>
<meta charset="utf-8">
<title>Checking Drive Support</title>
<style>
table th { border: 1px solid; }
table td { border: 1px solid; }
</style>
</head>
<body>
<hr />
<h2>title: Checking Drive Support
description: The following example examines disc device characteristics that are independent of media inserted in the device.
ms.assetid: 2d7e5ff9-7f1b-4bc1-bbc8-5e7eab45cdb0
ms.topic: article
ms.date: 05/31/2018</h2>
<h1>Checking Drive Support</h1>
<p>The following example examines disc device characteristics that are independent of media inserted in the device. More specifically, it retrieves lists of supported features, supported profiles, and supported mode pages, as well as the current feature settings and profile.</p>
<pre lang="VB"><code>' This script examines the burn device characteristics such as 
' product ID, product revision level, feature set, and profiles. 

' Copyright (C) Microsoft Corp. 2006

Option Explicit

' Constants
const IMAPI_FEATURE_PAGE_TYPE_DVD_DASH_WRITE = 47

WScript.Quit(Main)

Function Main
    Dim Index                ' Index to recording drive.
    Dim Recorder             ' Recorder object
    Dim Path                 ' Directory of files to burn
    Dim Stream               ' Data stream for burning device
    
    Index = 1                ' Second drive on the system

    ' Create a DiscMaster2 object to connect to CD/DVD drives.
    Dim g_DiscMaster
    Set g_DiscMaster = WScript.CreateObject(&quot;IMAPI2.MsftDiscMaster2&quot;)

    ' Create a DiscRecorder object for the specified burning device.
    Dim uniqueId
    set recorder = WScript.CreateObject(&quot;IMAPI2.MsftDiscRecorder2&quot;)
    uniqueId = g_DiscMaster.Item(Index)
    recorder.InitializeDiscRecorder( uniqueId )

    ' *** - Formatting to display recorder info
    WScript.Echo &quot;--------------------------------------------------&quot;
    Wscript.Echo &quot; ActiveRecorderId: &quot; &amp; recorder.ActiveDiscRecorder
    Wscript.Echo &quot;        Vendor Id: &quot; &amp; recorder.VendorId
    Wscript.Echo &quot;       Product Id: &quot; &amp; recorder.ProductId
    Wscript.Echo &quot; Product Revision: &quot; &amp; recorder.ProductRevision
    Wscript.Echo &quot;       VolumeName: &quot; &amp; recorder.VolumeName
    Wscript.Echo &quot;   Can Load Media: &quot; &amp; recorder.DeviceCanLoadMedia
    Wscript.Echo &quot;    Device Number: &quot; &amp; recorder.LegacyDeviceNumber

    Dim mountPoint
    For Each mountPoint In recorder.VolumePathNames
        WScript.Echo &quot;      Mount Point: &quot; &amp; mountPoint
    Next

    WScript.Echo &quot;Supported Features&quot;  'in _IMAPI_FEATURE_PAGE_TYPE 
    Dim supportedFeature
    For Each supportedFeature  in recorder.SupportedFeaturePages
        if supportedFeature = _
            IMAPI_FEATURE_PAGE_TYPE_DVD_DASH_WRITE then
                WScript.Echo &quot;    Feature: &quot; &amp; supportedFeature &amp; _
                    &quot;  Drive supports DVD-RW.&quot;
        else
                     WScript.Echo &quot;    Feature: &quot; &amp; supportedFeature
        end if
    Next
    
    WScript.Echo &quot;Current Features&quot;    'in _IMAPI_FEATURE_PAGE_TYPE
    Dim currentFeature
    For Each currentFeature  in recorder.CurrentFeaturePages
        WScript.Echo &quot;    Feature: &quot; &amp; currentFeature
    Next
    
    WScript.Echo &quot;Supported Profiles&quot;  'in _IMAPI_PROFILE_TYPE
    Dim supportedProfile
    For Each supportedProfile  in recorder.SupportedProfiles
        WScript.Echo &quot;    Profile: &quot; &amp; supportedProfile
    Next

    WScript.Echo &quot;Current Profiles&quot;    'in _IMAPI_PROFILE_TYPE
    Dim currentProfile
    For Each currentProfile    in recorder.CurrentProfiles
        WScript.Echo &quot;    Profile: &quot; &amp; currentProfile
    Next
    
    WScript.Echo &quot;Supported Mode Pages&quot;  'in  _IMAPI_MODE_PAGE_TYPE
    Dim supportedModePage       
    For Each supportedModePage in recorder.SupportedModePages
        WScript.Echo &quot;    Mode Page: &quot; &amp; supportedModePage
    Next

    WScript.Echo
    WScript.Echo &quot;----- Finished content -----&quot;
    Main = 0
End Function
</code></pre>
<h2>Related topics</h2>
<!-- raw HTML omitted -->
<p><a href="using-imapi.html">Using IMAPI</a></p>
<!-- raw HTML omitted -->
<p><a href="/windows/desktop/api/imapi2/nn-imapi2-idiscmaster2"><strong>IDiscMaster2</strong></a></p>
<!-- raw HTML omitted -->
<p><a href="/windows/desktop/api/imapi2/nn-imapi2-idiscrecorder2"><strong>IDiscRecorder2</strong></a></p>
<!-- raw HTML omitted -->
<p><a href="/windows/desktop/api/imapi2/ne-imapi2-imapi_feature_page_type"><strong>IMAPI_FEATURE_PAGE_TYPE</strong></a></p>
<!-- raw HTML omitted -->
<p><a href="/windows/desktop/api/imapi2/ne-imapi2-imapi_mode_page_type"><strong>IMAPI_MODE_PAGE_TYPE</strong></a></p>
<!-- raw HTML omitted -->
<p><a href="/windows/desktop/api/imapi2/ne-imapi2-imapi_profile_type"><strong>IMAPI_PROFILE_TYPE</strong></a></p>
<!-- raw HTML omitted -->
<p>Â </p>
<p>Â </p>
</body>
