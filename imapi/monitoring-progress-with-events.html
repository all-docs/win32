<!DOCTYPE html>
<html lang="en">
<head>
<meta charset="utf-8">
<title>Monitoring Progress With Events</title>
<style>
table th { border: 1px solid; }
table td { border: 1px solid; }
</style>
</head>
<body>
<hr />
<h2>title: Monitoring Progress With Events
description: Several interfaces let you implement an event handler to receive progress information. For example, an event object can be attached to the data writer to receive status of the write operation.
ms.assetid: 1f15a5fe-f5d7-4e09-805f-2d0380bf2bb2
ms.topic: article
ms.date: 05/31/2018</h2>
<h1>Monitoring Progress With Events</h1>
<p>Several interfaces let you implement an event handler to receive progress information. For example, an event object can be attached to the data writer to receive status of the write operation.</p>
<p>An event handler is implemented as a subroutine in a script. The following example shows how to define the subroutine and use the <strong>WScript.ConnectObject</strong> method to connect the event handler to the object.</p>
<pre lang="VB"><code>    ' Create the MsftDiscFormat2Data object (see the IDiscFormat2Data interface).
    Set dataWriter = CreateObject (&quot;IMAPI2.MsftDiscFormat2Data&quot;)

    ' Attach event handler (see the DDiscFormat2DataEvents interface).
    ' The &quot;dwBurnEvent_&quot; parameter identifies the handler (see the 
    ' dwBurnEvent_Update subroutine).
    WScript.ConnectObject  dataWriter, &quot;dwBurnEvent_&quot;

' Event handler for the MsftDiscFormat2Data.Write method.
' For details of the subroutine's parameters, see the 
' DDiscFormat2DataEvents::Update method.
SUB dwBurnEvent_Update( byRef object, byRef progress )
    ' --- Other code occurs here. 
END SUB

</code></pre>
<p>The name specified for the event handler name must contain the underscore suffix (&quot;_&quot;). To form the subroutine name, concatenate the method name to the event handler name. For example, if you use &quot;dataWriterEvent_&quot; as the event handler name and the method name is &quot;Update&quot;, the subroutine name would be dataWriterEvent_Update.</p>
<p>The following example shows an alternative approach to connecting the event handler to the object.</p>
<pre lang="VB"><code>    ' Create object and connect the event handler in one step.
    '   Set dataWriter = _
    '   WScript.CreateObject(&quot;IMAPI2.MsftDiscFormat2Data&quot;,&quot;dwBurnEvent_&quot;)


' Event handler  
SUB dwBurnEvent_Update( byRef object, byRef progress )
    '--- Other code occurs here. 
END SUB
</code></pre>
<p>If a system contains a second burn device to monitor, you would need to create another <strong>MsftDiscFormat2Data</strong> object and event handler.</p>
<p>The following example builds on the <a href="burning-a-disc.html">Burning a Disc Image</a> example. The example writes an ISO image to a blank disc and uses an event handler to give progress updates.</p>
<pre lang="VB"><code>' This script burns data files to disc in a single session 
' using files from a single directory tree. 

' Copyright (C) Microsoft Corp. 2006

Option Explicit

' *** CD/DVD disc file system types
Const FsiFileSystemISO9660 = 1
Const FsiFileSystemJoliet  = 2
Const FsiFileSystemUDF102  = 4

' *** IFormat2Data Write Action Enumerations
Const IMAPI_FORMAT2_DATA_WRITE_ACTION_VALIDATING_MEDIA      = 0
Const IMAPI_FORMAT2_DATA_WRITE_ACTION_FORMATTING_MEDIA      = 1
Const IMAPI_FORMAT2_DATA_WRITE_ACTION_INITIALIZING_HARDWARE = 2
Const IMAPI_FORMAT2_DATA_WRITE_ACTION_CALIBRATING_POWER     = 3
Const IMAPI_FORMAT2_DATA_WRITE_ACTION_WRITING_DATA          = 4
Const IMAPI_FORMAT2_DATA_WRITE_ACTION_FINALIZATION          = 5
Const IMAPI_FORMAT2_DATA_WRITE_ACTION_COMPLETED             = 6
const IMAPI_FORMAT2_DATA_WRITE_ACTION_VERIFYING             = 7

WScript.Quit(Main)

Function Main
    Dim Index                ' Index to recording drive.
    Dim Recorder             ' Recorder object
    Dim Path                 ' Directory of files to burn
    Dim Stream               ' Data stream for burning device
    
    Index = 1                ' Second drive on the system
    Path = &quot;c:\test\imt\data\2tracks&quot;      ' Files to transfer to disc

    ' Create a DiscMaster2 object to connect to CD/DVD drives.
    Dim g_DiscMaster
    Set g_DiscMaster = WScript.CreateObject(&quot;IMAPI2.MsftDiscMaster2&quot;)

    ' Create a DiscRecorder object for the specified burning device.
    Dim uniqueId
    set recorder = WScript.CreateObject(&quot;IMAPI2.MsftDiscRecorder2&quot;)
    uniqueId = g_DiscMaster.Item(index)
    recorder.InitializeDiscRecorder( uniqueId )

    ' Create an image stream for a specified directory.
    Dim FSI                  'Disc file system
    Dim Dir                  'Root directory of the disc file system
    Dim dataWriter    
        
    ' Create a new file system image and retrieve root directory
    Set FSI = CreateObject(&quot;IMAPI2FS.MsftFileSystemImage&quot;)
    Set Dir = FSI.Root

    ' Define the new disc format and set the recorder
    Set dataWriter = CreateObject (&quot;IMAPI2.MsftDiscFormat2Data&quot;)
    dataWriter.recorder = Recorder
    dataWriter.ClientName = &quot;IMAPIv2 TEST&quot;

    FSI.FreeMediaBlocks = dataWriter.FreeSectorsOnMedia
    FSI.FileSystemsToCreate = FsiFileSystemISO9660

    ' Add the directory and its contents to the file system 
    Dir.AddTree Path, false
        
    ' Create an image from the file system
    Dim result
    Set result = FSI.CreateResultImage()
    Stream = result.ImageStream
    
    ' Attach event handler to the data writing object.
    WScript.ConnectObject  dataWriter, &quot;dwBurnEvent_&quot;

    ' Specify the recorder and write the stream to disc.
    WScript.Echo &quot;Writing content to disc...&quot;
    dataWriter.write(Stream)

    WScript.Echo &quot;----- Finished writing content -----&quot;
    Main = 0
End Function

' Event handler - Progress updates when writing data

SUB dwBurnEvent_Update( byRef object, byRef progress )
    DIM strTimeStatus
    strTimeStatus = &quot;Time: &quot; &amp; progress.ElapsedTime &amp; _
        &quot; / &quot; &amp; progress.TotalTime
   
    SELECT CASE progress.CurrentAction
    CASE IMAPI_FORMAT2_DATA_WRITE_ACTION_VALIDATING_MEDIA
        WScript.Echo &quot;Validating media &quot; &amp; strTimeStatus

    CASE IMAPI_FORMAT2_DATA_WRITE_ACTION_FORMATTING_MEDIA
        WScript.Echo &quot;Formatting media &quot; &amp; strTimeStatus
        
    CASE IMAPI_FORMAT2_DATA_WRITE_ACTION_INITIALIZING_HARDWARE
        WScript.Echo &quot;Initializing Hardware &quot; &amp; strTimeStatus

    CASE IMAPI_FORMAT2_DATA_WRITE_ACTION_CALIBRATING_POWER
        WScript.Echo &quot;Calibrating Power (OPC) &quot; &amp; strTimeStatus

    CASE IMAPI_FORMAT2_DATA_WRITE_ACTION_WRITING_DATA
        DIM totalSectors, writtenSectors, percentDone
        totalSectors = progress.SectorCount
        writtenSectors = progress.LastWrittenLba - progress.StartLba
        percentDone = FormatPercent(writtenSectors/totalSectors)
        WScript.Echo &quot;Progress:  &quot; &amp; percentDone &amp; &quot;  &quot; &amp; strTimeStatus

    CASE IMAPI_FORMAT2_DATA_WRITE_ACTION_FINALIZATION
        WScript.Echo &quot;Finishing the writing &quot; &amp; strTimeStatus
    
    CASE IMAPI_FORMAT2_DATA_WRITE_ACTION_COMPLETED
        WScript.Echo &quot;Completed the burn.&quot;

    CASE IMAPI_FORMAT2_DATA_WRITE_ACTION_VERIFYING
        WScript.Echo &quot;Verifying the data.&quot;

    CASE ELSE
        WScript.Echo &quot;Unknown action: &quot; &amp; progress.CurrentAction
    END SELECT
END SUB
</code></pre>
<h2>Related topics</h2>
<!-- raw HTML omitted -->
<p><a href="using-imapi.html">Using IMAPI</a></p>
<!-- raw HTML omitted -->
<p><a href="/windows/desktop/api/objidl/nn-objidl-istream"><strong>IStream</strong></a></p>
<!-- raw HTML omitted -->
<p><a href="/windows/desktop/api/imapi2/nn-imapi2-idiscmaster2"><strong>IDiscMaster2</strong></a></p>
<!-- raw HTML omitted -->
<p><a href="/windows/desktop/api/imapi2/nn-imapi2-idiscformat2data"><strong>IDiscFormat2Data</strong></a></p>
<!-- raw HTML omitted -->
<p><a href="/windows/desktop/api/imapi2fs/nn-imapi2fs-ifilesystemimage"><strong>IFileSystemImage</strong></a></p>
<!-- raw HTML omitted -->
<p>Â </p>
<p>Â </p>
</body>
