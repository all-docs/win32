<!DOCTYPE html>
<html lang="en">
<head>
<meta charset="utf-8">
<title>Checking Media Support</title>
<style>
table th { border: 1px solid; }
table td { border: 1px solid; }
</style>
</head>
<body>
<hr />
<h2>title: Checking Media Support
description: The following script examines characteristics of the media loaded in the disc device.
ms.assetid: 05d88612-ff4c-4894-b838-a1d76923430c
ms.topic: article
ms.date: 05/31/2018</h2>
<h1>Checking Media Support</h1>
<p>The following script examines characteristics of the media loaded in the disc device. More specifically, it checks the media type and the current state of the media, as well as recorder and media compatibility. This script can be adapted to verify compatibility and state issues before using the media.</p>
<pre lang="VB"><code>' This script examines characteristics of the media loaded in the disc device.

' Copyright (C) Microsoft Corp. 2006

Option Explicit

' *** IMAPI2 Media Types 
Const IMAPI_MEDIA_TYPE_UNKNOWN            = 0  ' Media not present OR
                                               ' is unrecognized
Const IMAPI_MEDIA_TYPE_CDROM              = 1  ' CD-ROM
Const IMAPI_MEDIA_TYPE_CDR                = 2  ' CD-R
Const IMAPI_MEDIA_TYPE_CDRW               = 3  ' CD-RW
Const IMAPI_MEDIA_TYPE_DVDROM             = 4  ' DVD-ROM
Const IMAPI_MEDIA_TYPE_DVDRAM             = 5  ' DVD-RAM
Const IMAPI_MEDIA_TYPE_DVDPLUSR           = 6  ' DVD+R
Const IMAPI_MEDIA_TYPE_DVDPLUSRW          = 7  ' DVD+RW
Const IMAPI_MEDIA_TYPE_DVDPLUSR_DUALLAYER = 8  ' DVD+R dual layer
Const IMAPI_MEDIA_TYPE_DVDDASHR           = 9  ' DVD-R
Const IMAPI_MEDIA_TYPE_DVDDASHRW          = 10 ' DVD-RW
Const IMAPI_MEDIA_TYPE_DVDDASHR_DUALLAYER = 11 ' DVD-R dual layer
Const IMAPI_MEDIA_TYPE_DISK               = 12 ' Randomly writable

' *** IMAPI2 Data Media States
Const IMAPI_FORMAT2_DATA_MEDIA_STATE_UNKNOWN            = 0
Const IMAPI_FORMAT2_DATA_MEDIA_STATE_INFORMATIONAL_MASK = 15    
Const IMAPI_FORMAT2_DATA_MEDIA_STATE_UNSUPPORTED_MASK   = 61532 '0xfc00
Const IMAPI_FORMAT2_DATA_MEDIA_STATE_OVERWRITE_ONLY     = 1
Const IMAPI_FORMAT2_DATA_MEDIA_STATE_BLANK              = 2
Const IMAPI_FORMAT2_DATA_MEDIA_STATE_APPENDABLE         = 4     
Const IMAPI_FORMAT2_DATA_MEDIA_STATE_FINAL_SESSION      = 8
Const IMAPI_FORMAT2_DATA_MEDIA_STATE_DAMAGED            = 1024 '0x400
Const IMAPI_FORMAT2_DATA_MEDIA_STATE_ERASE_REQUIRED     = 2048 '0x800
Const IMAPI_FORMAT2_DATA_MEDIA_STATE_NON_EMPTY_SESSION  = 4096 '0x1000
Const IMAPI_FORMAT2_DATA_MEDIA_STATE_WRITE_PROTECTED    = 8192 '0x2000
Const IMAPI_FORMAT2_DATA_MEDIA_STATE_FINALIZED          = 16384 '0x4000
Const IMAPI_FORMAT2_DATA_MEDIA_STATE_UNSUPPORTED_MEDIA  = 32768 '0x8000


WScript.Quit(Main)

Function Main
    Dim Index                ' Index to recording drive.
    Dim recorder             ' Recorder object
    Dim Stream               ' Data stream for burning device
    
    Index = 1                ' Second drive on the system

    ' Create a DiscMaster2 object to connect to CD/DVD drives.
    Dim g_DiscMaster
    Set g_DiscMaster = WScript.CreateObject(&quot;IMAPI2.MsftDiscMaster2&quot;)

    ' Create a DiscRecorder object for the specified burning device.
    Dim uniqueId
    set recorder = WScript.CreateObject(&quot;IMAPI2.MsftDiscRecorder2&quot;)
    uniqueId = g_DiscMaster.Item(index)
    recorder.InitializeDiscRecorder( uniqueId )

    ' Define the new disc format and set the recorder
    Dim dataWriter
    Set dataWriter = CreateObject (&quot;IMAPI2.MsftDiscFormat2Data&quot;)
    dataWriter.recorder = recorder

    Dim boolResult
    boolResult = dataWriter.IsRecorderSupported(recorder)
    If boolResult = True then
        WScript.Echo &quot;--- Current recorder IS supported. ---&quot;
    else
        WScript.Echo &quot;Current recorder IS NOT supported.&quot;
    end if

    boolResult = dataWriter.IsCurrentMediaSupported(recorder)
    If boolResult = True then
        WScript.Echo &quot;--- Current media IS supported. ---&quot;
    else
        WScript.Echo &quot;Current media IS NOT supported.&quot;
    end if

    WScript.Echo &quot;ClientName = &quot; &amp; dataWriter.ClientName

    ' Check a few CurrentMediaStatus possibilities. Each status
    ' is associated with a bit and some combinations are legal.
    Dim curMediaStatus
    curMediaStatus = dataWriter.CurrentMediaStatus
    WScript.Echo &quot;Checking Current Media Status&quot;
    
    if IMAPI_FORMAT2_DATA_MEDIA_STATE_UNKNOWN AND curMediaStatus then
        WScript.Echo &quot;    Media state is unknown.&quot;
    end if

    if IMAPI_FORMAT2_DATA_MEDIA_STATE_OVERWRITE_ONLY AND curMediaStatus then
        WScript.Echo &quot;    Currently, only overwriting is supported.&quot;
    end if
    
    if IMAPI_FORMAT2_DATA_MEDIA_STATE_APPENDABLE AND curMediaStatus then
        WScript.Echo &quot;    Media is currently appendable.&quot;
    end if
    
    if IMAPI_FORMAT2_DATA_MEDIA_STATE_FINAL_SESSION AND curMediaStatus then
        WScript.Echo &quot;    Media is in final writing session.&quot;
    end if
    
    if IMAPI_FORMAT2_DATA_MEDIA_STATE_DAMAGED AND curMediaStatus then
        WScript.Echo &quot;    Media is damaged.&quot;
    end if
    
    Dim mediaType
    mediaType = dataWriter.CurrentPhysicalMediaType
    WScript.Echo &quot;Current Media Type&quot;
    DisplayMediaType(MediaType)

    WScript.Echo 
    WScript.Echo &quot;----- Finished -----&quot;
    Main = 0
End Function

Sub DisplayMediaType(dMediaType)
    Select Case dmediaType 
        Case IMAPI_MEDIA_TYPE_UNKNOWN
            WScript.Echo &quot;    Empty device or an unknown disc type.&quot;
        
        Case IMAPI_MEDIA_TYPE_CDROM
            WScript.Echo &quot;    CD-ROM&quot;
        
        Case IMAPI_MEDIA_TYPE_CDR
            WScript.Echo &quot;    CD-R&quot;
        
        Case IMAPI_MEDIA_TYPE_CDRW
            WScript.Echo &quot;    CD-RW&quot;
        
        Case IMAPI_MEDIA_TYPE_DVDROM
            WScript.Echo &quot;    Read-only DVD drive and/or disc&quot;
        
        Case IMAPI_MEDIA_TYPE_DVDRAM
            WScript.Echo &quot;    DVD-RAM&quot;
        
        Case IMAPI_MEDIA_TYPE_DVDPLUSR
            WScript.Echo &quot;    DVD+R&quot;
        
        Case IMAPI_MEDIA_TYPE_DVDPLUSRW
            WScript.Echo &quot;    DVD+RW&quot;
        
        Case IMAPI_MEDIA_TYPE_DVDPLUSR_DUALLAYER
            WScript.Echo &quot;    DVD+R Dual Layer media&quot;
        
        Case IMAPI_MEDIA_TYPE_DVDDASHR
            WScript.Echo &quot;    DVD-R&quot;
        
        Case IMAPI_MEDIA_TYPE_DVDDASHRW
            WScript.Echo &quot;    DVD-RW&quot;
        
        Case IMAPI_MEDIA_TYPE_DVDDASHR_DUALLAYER
            WScript.Echo &quot;    DVD-R Dual Layer media&quot;
        
        Case IMAPI_MEDIA_TYPE_DISK
            WScript.Echo &quot;    Randomly-writable, hardware-defect &quot; _
                + &quot;managed media type &quot;
            WScript.Echo &quot;    that reports the &quot;&quot;Disc&quot;&quot; profile &quot; _
                + &quot;as current.&quot;
        End Select
End Sub
</code></pre>
<h2>Related topics</h2>
<!-- raw HTML omitted -->
<p><a href="using-imapi.html">Using IMAPI</a></p>
<!-- raw HTML omitted -->
<p><a href="/windows/desktop/api/imapi2/ne-imapi2-imapi_media_physical_type"><strong>IMAPI_MEDIA_PHYSICAL_TYPE</strong></a></p>
<!-- raw HTML omitted -->
<p><a href="/windows/desktop/api/imapi2/nn-imapi2-idiscmaster2"><strong>IDiscMaster2</strong></a></p>
<!-- raw HTML omitted -->
<p><a href="/windows/desktop/api/imapi2/nn-imapi2-idiscrecorder2"><strong>IDiscRecorder2</strong></a></p>
<!-- raw HTML omitted -->
<p><a href="/windows/desktop/api/imapi2/nn-imapi2-idiscformat2data"><strong>IDiscFormat2Data</strong></a></p>
<!-- raw HTML omitted -->
<p>Â </p>
<p>Â </p>
</body>
