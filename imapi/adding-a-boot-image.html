<!DOCTYPE html>
<html lang="en">
<head>
<meta charset="utf-8">
<title>Adding a Boot Image</title>
<style>
table th { border: 1px solid; }
table td { border: 1px solid; }
</style>
</head>
<body>
<hr />
<h2>title: Adding a Boot Image
description: This example builds on the Burning a Disc Image example by adding code to include a bootable image in the boot section of the disc.
ms.assetid: b23cdbb9-ae0d-4261-965b-56abe865f323
ms.topic: article
ms.date: 05/31/2018</h2>
<h1>Adding a Boot Image</h1>
<p>This example builds on the <a href="burning-a-disc.html">Burning a Disc Image</a> example by adding code to include a bootable image in the boot section of the disc. The bootable image connects to the file system object that is written to disc. Once attached, the rest of the burning process is identical to the basic burning procedure. The boot image provides system startup using the CD or DVD disc drive.</p>
<p>The example hardcodes the path to the bootable image. Be sure to change the path along with other hardcoded values as appropriate.</p>
<pre lang="VB"><code>' This script burns a boot image and data files to disc in a 
' single session  using files from a single directory tree. 

'Copyright (C) Microsoft Corp. 2006

Option Explicit

' *** CD/DVD disc file system types
Const FsiFileSystemISO9660 = 1
Const FsiFileSystemJoliet  = 2
Const FsiFileSystemUDF  = 4

WScript.Quit(Main)

Function Main
    Dim index                ' Index to recording drive.
    Dim recorder             ' Recorder object
    Dim path                 ' Directory of files to burn
    Dim stream               ' Data stream for burning device
    Dim bootFile             ' Path and filename of boot image
    
    index = 1                ' Second drive on the system
    path = &quot;g:\BurnDir&quot;      
    bootFile = &quot;g:\BootImg\etfsboot.com&quot;

    ' Create a DiscMaster2 object to connect to CD/DVD drives.
    Dim g_DiscMaster
    Set g_DiscMaster = WScript.CreateObject(&quot;IMAPI2.MsftDiscMaster2&quot;)

    ' Create a DiscRecorder object for the specified burning device.
    Dim uniqueId
    set recorder = WScript.CreateObject(&quot;IMAPI2.MsftDiscRecorder2&quot;)
    uniqueId = g_DiscMaster.Item(index)
    recorder.InitializeDiscRecorder( uniqueId )

    ' -------- Adding Boot Image Code -----
    Dim bootOptions    
    WScript.Echo &quot;Creating BootOptions&quot;
    SET bootOptions = WScript.CreateObject(&quot;IMAPI2FS.BootOptions&quot;)
    bootOptions.Manufacturer = &quot;Microsoft&quot;
    bootOptions.PlatformId   = 0       ' x86 processor
    bootOptions.Emulation    = 0       ' EmulationType.EmulationNone
    
    ' Need a stream for the boot image file 
    Const adFileTypeBinary = 1
    DIM bootStream
    Set bootStream = CreateObject(&quot;ADODB.Stream&quot;)
    WScript.Echo &quot;Creating IStream for file &quot; + bootFile
    bootStream.Open
    bootStream.Type = adFileTypeBinary
    bootStream.LoadFromFile bootFile
    bootOptions.AssignBootImage(bootStream)
    
    ' Create disc file system image (ISO9660 in this example)
    Dim FSI
    SET FSI = WScript.CreateObject(&quot;IMAPI2FS.MsftFileSystemImage&quot;)
    FSI.ChooseImageDefaults(Recorder)
   
    ' Add the boot directory and its contents to the file system 
    FSI.BootImageOptions = bootOptions

    ' Add the content directory and files to the file system 
    FSI.root.AddTree path, FALSE

    Dim result
    Set result = FSI.CreateResultImage()
    stream = result.ImageStream

    ' Create and write stream to disc using the specified recorder.
    Dim dataWriter
    Set dataWriter = CreateObject(&quot;IMAPI2.MsftDiscFormat2Data&quot;)
    dataWriter.recorder = Recorder
    dataWriter.ClientName = &quot;IMAPIv2 TEST&quot;

    dataWriter.write(stream)
    WScript.Echo &quot;----- Finished writing content -----&quot;

    Main = 0
End Function

</code></pre>
<h2>Related topics</h2>
<!-- raw HTML omitted -->
<p><a href="using-imapi.html">Using IMAPI</a></p>
<!-- raw HTML omitted -->
<p><a href="/windows/desktop/api/imapi2/nn-imapi2-idiscmaster2"><strong>IDiscMaster2</strong></a></p>
<!-- raw HTML omitted -->
<p><a href="/windows/desktop/api/imapi2/nn-imapi2-idiscformat2data"><strong>IDiscFormat2Data</strong></a></p>
<!-- raw HTML omitted -->
<p><a href="/windows/desktop/api/imapi2fs/nn-imapi2fs-ifilesystemimage"><strong>IFileSystemImage</strong></a></p>
<!-- raw HTML omitted -->
<p>Â </p>
<p>Â </p>
</body>
