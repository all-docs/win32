<!DOCTYPE html>
<html lang="en">
<head>
<meta charset="utf-8">
<title>Creating and Updating Active Directory Objects</title>
<style>
table th { border: 1px solid; }
table td { border: 1px solid; }
</style>
</head>
<body>
<hr />
<p>description: Active Directory objects can be used to locate resources in a computer network domain, such as users, security policies, printers, distributed components, and other resources.
ms.assetid: 96f89537-9419-495e-819c-fd07ba546620
ms.tgt_platform: multiple
title: Creating and Updating Active Directory Objects
ms.topic: article
ms.date: 05/31/2018
topic_type:</p>
<ul>
<li>kbArticle
api_name:
api_type:
api_location:</li>
</ul>
<hr />
<h1>Creating and Updating Active Directory Objects</h1>
<p>Active Directory objects can be used to locate resources in a computer network domain, such as users, security policies, printers, distributed components, and other resources. Active Directory objects can be created and updated using WMI. You can update an Active Directory object when new information about the object becomes available by using WMI event notification. For example, once an Active Directory user object is created, you can detect its creation with an event query in WMI and when the event is received, you can update the object with new information.</p>
<p>The following code example creates a new WMI instance of the class that represents the Active Directory user object. The example shows how to assign values to various properties required to create the new Active Directory user instance.</p>
<pre lang="VB"><code>Const cUserID = &quot;WMIUser&quot;
Const cComputerName = &quot;LocalHost&quot;
Const cWMInamespace = &quot;root/directory/LDAP&quot;
Const cWMIclass = &quot;ds_user&quot;

Const ADS_UF_ACCOUNTDISABLE = &amp;h000002

Set objWMILocator = _
    CreateObject(&quot;WbemScripting.SWbemLocator&quot;)
objWMILocator.Security_.AuthenticationLevel = _
    wbemAuthenticationLevelDefault

Set objWMIServices = objWMILocator. _
    ConnectServer(cComputerName, cWMInamespace, &quot;&quot;, &quot;&quot;)

Set objWMIClass = objWMIServices.Get(cWMIclass)

Set objWMIInstance = objWMIClass.SpawnInstance_

objWMIInstance.DS_sAMAccountName = userID
objWMIInstance.ADSIPath = &quot;LDAP://CN=&quot; &amp; userID &amp; _
    &quot;,CN=Users,DC=LissWare,DC=Net&quot;

objWMIInstance.Put_ (wbemChangeFlagCreateOrUpdate Or _
    wbemFlagReturnWhenComplete)

WScript.Echo &quot;Active Directory user created.&quot;
</code></pre>
<p>The following code example updates a WMI instance of an Active Directory object. In this example, the displayname attribute is updated.</p>
<pre lang="VB"><code>set svc = getObject(&quot;Winmgmts:root\directory\ldap&quot;)

' A context object is used to tell the provider which
' specific properties are going to be updated.  
' In most cases, when you update a WMI object you do not
' need to specify an additional context object. 
' However,  if a context object is not supplied for a
' directory service provider, the update fails.

set octx = createobject( _
    &quot;wbemscripting.swbemnamedvalueset&quot;)
octx.add &quot;__PUT_EXT_PROPERTIES&quot;, array(&quot;ds_displayname&quot;)
octx.add &quot;__PUT_EXTENSIONS&quot;, true
octx.add &quot;__PUT_EXT_CLIENT_REQUEST&quot;, true


set objEnum = svc.execQuery( _
    &quot;select * from ds_computer where ds_cn = 'userName'&quot;, &quot;WQL&quot;, 32)

for each obj in objEnum
 obj.ds_DisplayName = &quot;updatedName&quot;
 obj.put_ 1, octx
next

WScript.Echo &quot;Active Directory user successfully updated&quot;
</code></pre>
<p>Â </p>
<p>Â </p>
</body>
