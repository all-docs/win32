<!DOCTYPE html>
<html lang="en">
<head>
<meta charset="utf-8">
<title>Sending an Enveloped Data Message</title>
<style>
table th { border: 1px solid; }
table td { border: 1px solid; }
</style>
</head>
<body>
<hr />
<h2>description: In the following example, a plaintext message is read in from a file, and a certificate store that contains the certificates of the intended message recipients is opened.
ms.assetid: 7ae672d3-e11d-453c-b9c0-354d21830ae4
title: Sending an Enveloped Data Message
ms.topic: article
ms.date: 05/31/2018</h2>
<h1>Sending an Enveloped Data Message</h1>
<p>[CAPICOM is a 32-bit only component that is available for use in the following operating systems: Windows ServerÂ 2008, WindowsÂ Vista, and WindowsÂ XP. Instead, use the .NET Framework to implement security features. For more information, see <a href="alternatives-to-using-capicom.html">Alternatives to Using CAPICOM</a>.]</p>
<p>In the following example, a plaintext message is read in from a file, and a certificate store that contains the certificates of the intended message recipients is opened. All of the certificates in the store are added as recipients of the message, the message is enveloped, and the enveloped message is written to a file.</p>
<p>Additional code can be added to display the certificates being added as message recipients, to verify those certificates before they are added as intended recipients, or to otherwise choose those that are to be added.</p>
<p>On any CAPICOM error, a negative decimal value of <strong>Err.Number</strong> is returned. For more information, see <a href="capicom-error-code.html"><strong>CAPICOM_ERROR_CODE</strong></a>. For information about positive decimal values of <strong>Err.Number</strong>, see Winerror.h.</p>
<pre lang="VB"><code>Sub Envelope(ByVal InFile As String, ByVal OutFile As String, _
             ByVal StoreName As String)

On Error GoTo ErrorHandler

'Open the input file and read the message to be enveloped.

Dim Text As String
Open InFile For Input As #1
Input #1, Text
Close #1
If Len(Text) &lt; 1 Then
    MsgBox &quot;No message to be enveloped.&quot;
    Exit Sub
End If

'Open the store containing the certificates of
'the message recipients.

Dim CertStore As New Store
CertStore.Open CAPICOM_CURRENT_USER_STORE, StoreName, _
    CAPICOM_STORE_OPEN_READ_ONLY

' Check for an empty certificate store.

If CertStore.Certificates.Count &lt; 1 Then
    MsgBox &quot;There are no recipient certificates available.&quot;
    Set CertStore = Nothing
    Exit Sub
End If

' Declare and initialize an EnvelopedData object

Dim EnvMessage As New EnvelopedData
EnvMessage.Content = Text
Dim I As Integer
For I = 1 To CertStore.Certificates.Count
    EnvMessage.Recipients.Add CertStore.Certificates.Item(I)
    ' The following line can be uncommented to see a prompt
    ' for each certificate in the store.
    ' CertStore.Certificates.Item(I).Display
Next I

'  Set the encryption algorithm and key length. Comment out
'  or remove the following lines to use the default algorithm
'  and key length. The triple DES algorithm and 128 bit key
'  length may not be supported.
Envmessage.Algorithm.Name = ENCRYPTION_ALGORITHM_RC4
Envmessage.Algorithm.KeyLength = KEY_LENGTH_128_BITS

'Declare the string that will hold the enveloped message.

Dim EnvelopedMessage As String

'Encrypt the message into EnvelopedMessage. The enveloped message
'string contains everything that an intended recipient will need to
'decrypt the message, including information on the 
'encryption algorithm key length.
EnvelopedMessage = EnvMessage.Encrypt

' Optionally, check the length of the encrypted string to make sure 
' that the encrypt method worked.

If Len(EnvelopedMessage) &lt; 1 Then
    MsgBox &quot;no message encrypted. &quot;
Else
    MsgBox &quot; Message is &quot; &amp; Len(EnvelopedMessage) &amp; &quot; characters&quot;

    ' Open an output file and write the encrypted message to the 
    ' file. The file is not opened if there is no message to write.
    Open OutFile For Output As #2
    Write #2, EnvelopedMessage
    Close #2
    MsgBox &quot;The message written to file &quot;
End If

' Release the EncryptedData object and the Store object.
Set Envmessage = Nothing
Set CertStore = Nothing

Exit Sub

ErrorHandler:
If Err.Number &gt; 0 Then
    MsgBox &quot;Visual Basic error found : &quot; &amp; Err.Description
Else
    MsgBox &quot;CAPICOM error found : &quot; &amp; Err.Number
End If
End Sub
</code></pre>
<p>Â </p>
<p>Â </p>
</body>
