<!DOCTYPE html>
<html lang="en">
<head>
<meta charset="utf-8">
<title>Signing a Document</title>
<style>
table th { border: 1px solid; }
table td { border: 1px solid; }
</style>
</head>
<body>
<hr />
<h2>description: A standard use of a signature is to sign a text and save that signed text to a file. The signed text could also be sent over the Internet. The signed message is in PKCS #7 format.
ms.assetid: '67a36123-4fce-4d40-83c3-b9668221276b'
title: Signing a Document
ms.topic: article
ms.date: 05/31/2018</h2>
<h1>Signing a Document</h1>
<p>[CAPICOM is a 32-bit only component that is available for use in the following operating systems: Windows ServerÂ 2008, WindowsÂ Vista, and WindowsÂ XP. Instead, use the .NET Framework to implement security features. For more information, see <a href="alternatives-to-using-capicom.html">Alternatives to Using CAPICOM</a>.]</p>
<p>A standard use of a signature is to sign a text and save that signed text to a file. The signed text could also be sent over the Internet. The signed message is in PKCS #7 format.</p>
<p>In this example, the signature is created for detached content (when the content is not included with the signature). A detached signature would most often be used if the recipient of the signature has a copy of the exact signed text. In the example below, the original message and the detached signature are written to separate files.</p>
<p>On any CAPICOM error, a negative decimal value of <strong>Err.Number</strong> is returned. For more information, see <a href="capicom-error-code.html"><strong>CAPICOM_ERROR_CODE</strong></a>. For information about positive decimal values of <strong>Err.Number</strong>, see Winerror.h.</p>
<p>Creating a signature uses the signer's <a href="../secgloss/p-gly.html"><em>private key</em></a>. A signature can only be created if the signer's certificate with an associated private key is available. This example of the <strong>Sign</strong> method does not specify a signer. If a signer is not specified and no certificate in <strong>CAPICOM_MY_STORE</strong> has an associated private key, the <strong>Sign</strong> method fails. If one and only one certificate in <strong>CAPICOM_MY_STORE</strong> has an associated private key, that certificate and its private key is used to create the signature. If more than one certificate in the <strong>CAPICOM_MY_STORE</strong> store has an associated private key, a dialog box appears and the user can choose the certificate to be used to create the signature.</p>
<p>When a web-based application uses the <strong>Sign</strong> method, a prompt is always displayed and the user's permission is required before a signature that uses that signer's private key is created.</p>
<pre lang="VB"><code>Sub Signfile(ByVal InputFileName As String, _
    ByVal OutputFileName As String)
    
    On Error GoTo ErrorHandler
    Dim c As String
    Dim s As String
    Dim MyStore As New Store
    Dim Signobj As New SignedData
    Dim Signer As New Signer

    ' NOTE: the name 'Attribute' is not a unique name
    ' and must be preceded by 'CAPICOM.'
    Dim SigningTime As New CAPICOM.Attribute

    ' Open the MY store and retrieve the first certificate from the
    ' Store. The signing operation will only work if this
    ' certificate is valid and has access to the signer's private key.
    MyStore.Open CAPICOM_CURRENT_USER_STORE, &quot;MY&quot;, _
        CAPICOM_STORE_OPEN_READ_ONLY
    Signer.Certificate = MyStore.Certificates.Item(1)

    ' Open the input file and read the content to be signed from
    ' the file.
    Open App.Path &amp; &quot;\&quot; &amp; InputFileName For Input As #1
    Input #1, c
    Close #1
    
    ' Set the content to be signed.
    Signobj.Content = c

    ' Save the time the data was signed as a signer attribute.
    SigningTime.Name = CAPICOM_AUTHENTICATED_ATTRIBUTE_SIGNING_TIME
    SigningTime.Value = Now
    Signer.AuthenticatedAttributes.Add SigningTime

    ' Sign the content using the signer's private key.
    ' The 'True' parameter indicates that the content signed is not
    ' included in the signature string.
    s = Signobj.Sign(Signer, True)

    Open App.Path &amp; &quot;\&quot; &amp; OutputFileName For Output As #2
    Write #2, s
    Close #2

    MsgBox (&quot;Signature done - Saved to file&quot; &amp; OutputFileName)
    Set Signobj = Nothing
    Set MyStore = Nothing
    Set Signer = Nothing
    Set SigningTime = Nothing

    Exit Sub

ErrorHandler:
    If Err.Number &gt; 0 Then
        MsgBox (&quot;Visual Basic error found:&quot; &amp; Err.Description)
    Else
        MsgBox (&quot;CAPICOM error found : &quot; &amp; Err.Number)
    End If
End Sub
</code></pre>
<h2>Related topics</h2>
<!-- raw HTML omitted -->
<p><a href="store-open.html"><strong>Store.Open</strong></a></p>
<!-- raw HTML omitted -->
<p><a href="signer-certificate.html"><strong>Signer.Certificate</strong></a></p>
<!-- raw HTML omitted -->
<p><a href="attribute.html"><strong>Attribute</strong></a></p>
<!-- raw HTML omitted -->
<p><a href="signeddata.html"><strong>SignedData</strong></a></p>
<!-- raw HTML omitted -->
<p>Â </p>
<p>Â </p>
</body>
