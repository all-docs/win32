<!DOCTYPE html>
<html lang="en">
<head>
<meta charset="utf-8">
<title>Step 3: Reusing Components</title>
<style>
table th { border: 1px solid; }
table td { border: 1px solid; }
</style>
</head>
<body>
<hr />
<h2>description: 'Step 3: Reusing Components'
ms.assetid: d9c14cf8-5bc9-4a6c-9421-c16c3f41b10d
title: 'Step 3: Reusing Components'
ms.topic: article
ms.date: 05/31/2018</h2>
<h1>Step 3: Reusing Components</h1>
<h2>Objectives</h2>
<p>In this step, you will learn about the following:</p>
<ul>
<li>Reusable components.</li>
<li>How to plan for reusability.</li>
</ul>
<h2>Description</h2>
<p>Two previous parts of this COM+ services primer, <a href="step-1--creating-a-transactional-component.html">Step 1: Creating a Transactional Component</a> and <a href="step-2--extending-a-transaction-across-multiple-components.html">Step 2: Extending a Transaction Across Multiple Components</a> show how to write one component that calls a second component to assist in completing some work, updating author information in the Microsoft SQL Server Pubs database; all work is protected by a single transaction. The sample components focused on the work of updating an author's data and verifying the author's address, and COM+ provided transaction processing, <a href="com--just-in-time-activation.html">JIT activation</a>, and <a href="com--synchronization.html">concurrency protection</a>.</p>
<p>This step demonstrates how to reuse the components created in steps 1 and 2 and looks at what this means to the design of those components. As shown in the following illustration, this means creating a new component, <code>AddNewAuthor</code>, that adds new authors to the database by calling <code>UpdateAuthorAddress</code>.</p>
<p><img src="images/e746f50e-2e86-4e59-82f9-f407d2b0325c.png" alt="Diagram that shows the flow when reusing components." /></p>
<p>In addition to reusing existing component functionality, <code>AddNewAuthor</code> calls another new component called <code>ValidateAuthorName</code>. As the preceding illustration shows, <code>ValidateAuthorName</code> is non-transactional. The transaction attribute value for this component is left at its default setting (<strong>Not Supported</strong>) to exclude its work from the transaction. As shown in the step 3 sample code, <code>ValidateAuthorName</code> performs read-only queries on the database, and the failure of this minor task should not have the potential to abort the transaction. However, the transaction attribute value of the <code>AddNewAuthor</code> component is set to <strong>Required</strong>.</p>
<p>The <code>AddNewAuthor</code>, <code>UpdateAuthorAddress</code>, and <code>ValidateAuthorAddress</code> components all vote in the transaction. In this transaction, <code>AddNewAuthor</code> is the root object. COM+ always makes the first object created in the transaction the root object.</p>
<p>In this example, reusing the <code>UpdateAuthorAddress</code> component is easyâ€”COM+ automatically delivers the expected services. However, the results would be different if the transaction attribute value of the <code>UpdateAuthorAddress</code> component were initially set to <strong>Requires New</strong> instead of <strong>Required</strong>. On the surface, both settings appear similar; both guarantee a transaction. <strong>Requires New</strong>, however, always starts a new transaction, while <strong>Required</strong> starts a new transaction only when the object's caller is non-transactional. You can see from this how important it was to configure <code>UpdateAuthorAddress</code> carefully and thoughtfully. Otherwise, COM+ might have interpreted the service request differently, generating two unrelated transactions, as shown in the following illustration, instead of one.</p>
<p><img src="images/24b9edf6-af00-4994-8fa1-cee4ace16379.png" alt="Diagram that shows the flow when reusing components with &quot;Requires New&quot;." /></p>
<blockquote>
<p>[!Note]<br />
When you reuse components, make sure the services are configured to support your desired outcome.</p>
</blockquote>
<p>Â </p>
<h2>Sample code</h2>
<p>The AddNewAuthor component performs batch additions of new authors by allowing the object to remain active until the client releases its reference to the object.</p>
<pre lang="VB"><code>Option Explicit
'
'   Purpose:   This class is used for adding a new author.
'
'   Notes:    IMPT:  This component implicitly assumes that it will
'             always run in a transaction. Undefined results may 
'             otherwise occur.
'
'  AddNewAuthor
'
Public Sub AddNewAuthor( _
                        ByVal strAuthorFirstName As String, _
                        ByVal strAuthorLastName As String, _
                        ByVal strPhone As String, _
                        ByVal strAddress As String, _
                        ByVal strCity As String, _
                        ByVal strState As String, _
                        ByVal strZip As String, _
                        ByVal boolContracted As Boolean)
  ' Handle any errors.
  On Error GoTo UnexpectedError

  ' Verify component is in a transaction.
  ' The VerifyInTxn subroutine is described in Step 1.
  VerifyInTxn

  ' Get our object context.
  Dim objcontext As COMSVCSLib.ObjectContext
  Set objcontext = GetObjectContext

  ' Get the IContextState object.
  Dim contextstate As COMSVCSLib.IContextState
  Set contextstate = objcontext

  ' Validate that the author is OK.
  ' The ValidateAuthorName function is described after this function.
  Dim oValidateAuthName As Object
  Dim bValidAuthor As Boolean
  Set oValidateAuthName = CreateObject(&quot;ComplusPrimer.ValidateAuthorName&quot;)
  bValidAuthor = oValidateAuthName.ValidateAuthorName( _
    strAuthorFirstName, strAuthorLastName)
  If Not bValidAuthor Then
    Err.Raise 999999, &quot;The AddNewAuthor component&quot;, _
      &quot;You tried to add an author on the banned list!&quot;
  End If
  
  ' Open the connection to the database.
  Dim conn As ADODB.Connection
  Set conn = CreateObject(&quot;ADODB.Connection&quot;)

  ' Specify the OLE DB provider.
  conn.Provider = &quot;SQLOLEDB&quot;

  ' Connect using Windows Authentication.
  Dim strProv As String
  strProv = &quot;Server=MyDBServer;Database=pubs;Trusted_Connection=yes&quot;

  ' Open the database.
  conn.Open strProv

  ' Tell the database to actually add the author; use empty strings 
  ' for this part and rely on the UpdateAuthorAddress 
  ' component to validate the address/phone/etc data.
  ' Default Contract flag is off.
  Dim strUpdateString As String
  strUpdateString = &quot;insert into authors values(_
                     '789-65-1234',&quot; &amp; _
                     strAuthorLastName &amp; &quot;, &quot; &amp; _
                     strAuthorFirstName &amp; &quot;, &quot; &amp; _
                     &quot;'(555) 555-5555', ', ', ', '98765', &quot;

  If boolContracted Then
    strUpdateString = strUpdateString + &quot;1)&quot;
  Else
    strUpdateString = strUpdateString + &quot;0)&quot;
  End If

  conn.Execute strUpdateString
  
  ' Close the connection; this potentially allows 
  ' another component in the same transaction to
  ' reuse the connection from the connection pool.
  conn.Close
  Set conn = Nothing
  
  ' Create the UpdateAuthorAddress component.
  Dim oUpdateAuthAddr As Object
  Set oUpdateAuthAddr = CreateObject(&quot;ComplusPrimer.UpdateAuthorAddress&quot;)
  
  ' The component throws an error if anything goes wrong.
  oUpdateAuthAddr.UpdateAuthorAddress &quot;&quot;, strPhone, _
    strAddress, strCity, strState, strZip
    
  Set oUpdateAuthAddr = Nothing
  
  ' Everything works--commit the transaction.
  contextstate.SetMyTransactionVote TxCommit
  
  '  Design issue:  Allow batch additions of new
  '  authors in one transaction, or should each new author be added
  '  in a single new transaction?
  contextstate.SetDeactivateOnReturn False
  Exit Sub
  
UnexpectedError:
  ' There's an error.
  contextstate.SetMyTransactionVote TxAbort
  contextstate.SetDeactivateOnReturn True
  
End Sub
</code></pre>
<p>The <code>ValidateAuthorName</code> component validates author names before <code>AddNewAuthor</code> adds the name to the database. This component throws an error back to its caller if something unexpected happens.</p>
<pre lang="VB"><code>Option Explicit
'
'   Purpose:   This class is used for validating authors before
'              adding them to the database.
'
'   Notes:   This component doesn't need to be in a transaction because
'            it is performing read-only queries on the database,
'            especially since these queries are not overlapping with
'            any updates of end-user data. If an unexpected error
'            happens, let the error go back to the caller who
'            needs to handle it.
'

Public Function ValidateAuthorName( _
                        ByVal strAuthorFirstName As String, _
                        ByVal strAuthorLastName As String _
                        ) As Boolean
  ValidateAuthorName = False

  ' Open the connection to the database.
  Dim conn As ADODB.Connection
  Set conn = CreateObject(&quot;ADODB.Connection&quot;)

  ' Specify the OLE DB provider.
  conn.Provider = &quot;SQLOLEDB&quot;

  ' Connect using Windows Authentication.
  Dim strProv As String
  strProv = &quot;Server=MyDBServer;Database=pubs;Trusted_Connection=yes&quot;

  ' Open the database.
  conn.Open strProv

  ' Suppose another hypothetical table has been added to the Pubs 
  ' database, one that contains a list of banned authors.
  Dim rs As ADODB.Recordset
  Set rs = conn.Execute(&quot;select * from banned_authors&quot;)
  
  ' Loop through the banned-author list looking for the specified
  ' author.
  While Not rs.EOF
    If rs.Fields(&quot;FirstName&quot;) = strAuthorFirstName And _
      rs.Fields(&quot;LastName&quot;) = strAuthorLastName Then
        ' This is a banned author.
        conn.Close
        Set conn = Nothing
        Set rs = Nothing
        Exit Function
    End If
    rs.MoveNext
  Wend
  
  ' None of the added authors found in the banned list.
  ValidateAuthorName = True
  conn.Close
  Set conn = Nothing
  Set rs = Nothing
End Function
</code></pre>
<h2>Summary</h2>
<ul>
<li>There are times when you don't want a component to vote in the transaction.</li>
<li>COM+ does not enforce JIT activation or concurrency protection on non-transactional components. You may configure these services separately.</li>
<li>A calling component's configuration affects how COM+ interprets the service requests of the called component.</li>
</ul>
<h2>Related topics</h2>
<!-- raw HTML omitted -->
<p><a href="step-1--creating-a-transactional-component.html">Step 1: Creating a Transactional Component</a></p>
<!-- raw HTML omitted -->
<p><a href="step-2--extending-a-transaction-across-multiple-components.html">Step 2: Extending a Transaction Across Multiple Components</a></p>
<!-- raw HTML omitted -->
<p><a href="configuring-transactions.html">Configuring Transactions</a></p>
<!-- raw HTML omitted -->
<p><a href="designing-for-scalability.html">Designing for Scalability</a></p>
<!-- raw HTML omitted -->
<p>Â </p>
<p>Â </p>
</body>
