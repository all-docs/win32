<!DOCTYPE html>
<html lang="en">
<head>
<meta charset="utf-8">
<title>Configuring COM+ Application Recycling Values</title>
<style>
table th { border: 1px solid; }
table td { border: 1px solid; }
</style>
</head>
<body>
<hr />
<h2>description: You can use the following methods to configure application recycling values for your COM+ application.
ms.assetid: 8f6a4879-cf4c-4171-8448-bc07371e038c
title: Configuring COM+ Application Recycling Values
ms.topic: article
ms.date: 05/31/2018</h2>
<h1>Configuring COM+ Application Recycling Values</h1>
<p>You can use the following methods to configure application recycling values for your COM+ application.</p>
<blockquote>
<p>[!Note]<br />
You cannot recycle a COM+ application that has been configured to run as a Windows service. Also, library applications have the recycling and pooling properties of their host process.</p>
</blockquote>
<p>Â </p>
<h2>Component Services Administrative Tool</h2>
<p>To configure application recycling for a COM+ application, use the following steps:</p>
<ol>
<li>
<p>In the console tree of the Component Services administrative tool, right-click the COM+ server application you want to be recycled and then click <strong>Properties</strong>.</p>
</li>
<li>
<p>On the <strong>Pooling &amp; Recycling</strong> tab, enter values for <strong>Lifetime Limit (minutes)</strong>, <strong>Memory Limit (KB)</strong>, <strong>Expiration Timeout (minutes)</strong>, <strong>Call Limit</strong>, and <strong>Activation Limit</strong>, depending on the criteria you want to use.</p>
<ul>
<li><strong>Lifetime Limit</strong> indicates the maximum number of minutes a process can run before it's recycled. The valid range is 0 through 30,240 minutes (21 days). The default number of minutes is 0.</li>
<li><strong>Memory Limit</strong> indicates the maximum amount of process memory usage (in kilobytes) before recycling the process. If the process's memory usage exceeds the specified number for longer than one minute, the process is recycled. The valid range is 0 through 1,048,576 KB, and the default amount of memory usage is 0 KB.</li>
<li><strong>Expiration Timeout</strong> indicates the number of minutes to wait, before being forcibly shut down, for the release of all external references to objects in the process. The valid range is 1 through 1440 minutes (24 hours), and the default expiration time-out is 15 minutes. This value is used only when it is already determined that a process will be recycled based on the other criteria.</li>
<li><strong>Call Limit</strong> indicates the maximum number of calls that application objects can accept before recycling the process. The valid range is 0 through 1,048,576 calls, and the default number of calls is 0.</li>
<li><strong>Activation Limit</strong> indicates the maximum number of application object activations to accept before recycling the process. The valid range is 0 through 1,048,576 activations, and the default number of activations is 0.</li>
</ul>
<blockquote>
<p>[!Note]<br />
When the <strong>Lifetime Limit</strong>, <strong>Memory Limit</strong>, <strong>Call Limit</strong>, or <strong>Activation Limit</strong> value is set to 0 (the default value), application recycling for that criterion is disabled. When all four of these criteria are set to 0, application recycling is disabled for the selected application.</p>
</blockquote>
<p>Â </p>
</li>
<li>
<p>Click <strong>OK</strong>.</p>
</li>
</ol>
<h2>Visual Basic</h2>
<p>The following function in Microsoft Visual Basic demonstrates how you can set the application recycling values for any COM+ server application that you choose. To use it from Visual Basic, add a reference to the COM+ Admin Type Library.</p>
<pre lang="VB"><code>Function SetMyApplicationRecycling( _
  strApplicationName As String, _
  lngLifetimeLimit As Long, _
  lngMemoryLimit As Long, _
  lngCallLimit As Long, _
  lngActivationLimit As Long, _
  lngExpirationTimeout As Long _
) As Boolean  ' Return False if any errors occur.

    SetMyApplicationRecycling = False  ' Initialize the function.
    On Error GoTo My_Error_Handler  ' Initialize error handling.

    Dim objCatalog As COMAdmin.COMAdminCatalog
    Dim objAppCollection As COMAdmin.COMAdminCatalogCollection
    Dim objApplication As COMAdmin.COMAdminCatalogObject
    Set objCatalog = CreateObject(&quot;COMAdmin.COMAdminCatalog&quot;)
    Set objAppCollection = objCatalog.GetCollection(&quot;Applications&quot;)
    objAppCollection.Populate
    For Each objApplication In objAppCollection
        With objApplication
            If .Name = strApplicationName Then
                .Value(&quot;RecycleLifetimeLimit&quot;) = lngLifetimeLimit
                .Value(&quot;RecycleMemoryLimit&quot;) = lngMemoryLimit
                .Value(&quot;RecycleCallLimit&quot;) = lngCallLimit
                .Value(&quot;RecycleActivationLimit&quot;) = lngActivationLimit
                .Value(&quot;RecycleExpirationTimeout&quot;) = lngExpirationTimeout
                MsgBox strApplicationName &amp; _
                  &quot; recycling values are now set to the following: &quot; &amp; _
                  vbNewLine &amp; vbNewLine &amp; _
                  &quot;Lifetime Limit = &quot; &amp; lngLifetimeLimit &amp; vbNewLine &amp; _
                  &quot;Memory Limit = &quot; &amp; lngMemoryLimit &amp; vbNewLine &amp; _
                  &quot;Call Limit = &quot; &amp; lngCallLimit &amp; vbNewLine &amp; _
                  &quot;Activation Limit = &quot; &amp; lngActivationLimit &amp; vbNewLine _
                  &amp; &quot;Expiration Timeout = &quot; &amp; lngExpirationTimeout
                Exit For
            End If
        End With
    Next
    objAppCollection.SaveChanges
    Set objApplication = Nothing
    Set objAppCollection = Nothing
    Set objCatalog = Nothing
    SetMyApplicationRecycling = True  ' Successful end to procedure
    Exit Function
    
My_Error_Handler:  ' Replace with specific error handling.
    MsgBox &quot;Error # &quot; &amp; Err.Number &amp; &quot; (Hex: &quot; &amp; Hex(Err.Number) _
      &amp; &quot;)&quot; &amp; vbNewLine &amp; Err.Description
    Set objApplication = Nothing
    Set objAppCollection = Nothing
    Set objCatalog = Nothing
End Function

</code></pre>
<p>To use the function, provide a string value for the application name and integer values for the desired application recycling settings. The following Visual Basic code shows how to set the <strong>RecycleLifetimeLimit</strong> value to 5, the <strong>RecycleMemoryLimit</strong> value to 10, the <strong>RecycleCallLimit</strong> value to 9, the <strong>RecycleActivationLimit</strong> value to 100, and the <strong>RecycleExpirationTimeout</strong> value to 15.</p>
<pre lang="VB"><code>Sub Main()
    If Not SetMyApplicationRecycling(&quot;MyApp&quot;, 5, 10, 9, 100, 15) Then
        MsgBox &quot;SetMyApplicationRecycling failed.&quot;
    End If
End Sub

</code></pre>
<h2>Related topics</h2>
<!-- raw HTML omitted -->
<p><a href="com--application-recycling-concepts.html">COM+ Application Recycling Concepts</a></p>
<!-- raw HTML omitted -->
<p>Â </p>
<p>Â </p>
</body>
