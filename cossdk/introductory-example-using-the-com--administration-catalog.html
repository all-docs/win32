<!DOCTYPE html>
<html lang="en">
<head>
<meta charset="utf-8">
<title>Introductory Example Using the COM+ Administration Catalog</title>
<style>
table th { border: 1px solid; }
table td { border: 1px solid; }
</style>
</head>
<body>
<hr />
<h2>description: Introductory Example Using the COM+ Administration Catalog
ms.assetid: e9ce25aa-4fb1-4357-9f4e-5bf649e29447
title: Introductory Example Using the COM+ Administration Catalog
ms.topic: article
ms.date: 05/31/2018</h2>
<h1>Introductory Example Using the COM+ Administration Catalog</h1>
<p>When programmatically using the COM+ Administration catalog, you typically carry out the following general steps (not given in a strict order here):</p>
<ul>
<li>Open a session with the COM+ catalog on the local machine. Optionally, connect to the COM+ catalog on a remote machine.</li>
<li>Perform actions such as starting or stopping servicesâ€”actions that don't pertain to a particular COM+ application.</li>
<li>Perform actions such as installing or exporting COM+ applications, or installing components into applicationsâ€”actions that pertain to reading from or writing to files.</li>
<li>Add new items to collections, such as creating a new COM+ application by adding a new item to the &quot;Applications&quot; collection.</li>
<li>Set or get properties on an item in a collection.</li>
<li>Save or discard pending changes to the catalog.</li>
<li>Handle any errors that might occur.</li>
</ul>
<p>To show what these steps look like when you use the COMAdmin objects, a Microsoft Visual Basic example is provided below. It briefly illustrates some of the typical steps described above, such as locating collections, enumerating through a collection to retrieve an item, and setting properties on that item.</p>
<p>In the example below you will perform the following actions:</p>
<ol>
<li>Create a new COM+ application, &quot;MyHomeZoo&quot;.</li>
<li>Install some components, Cat and Dog, into the application. Both components are contained in a single DLL that must already exist: MyZoo.dll.</li>
<li>Configure role-based security for the application by defining two roles: ZooKeeper and AllergicToCats.</li>
<li>Assign the ZooKeeper role access to the entire application.</li>
<li>Assign the AllergicToCats role access to only the Dog component.</li>
<li>Turn on security properties so that role checking will be enforced for the application.</li>
<li>Export the MyHomeZoo application to a file so that it can be installed on other computers.</li>
</ol>
<p>To use this example from Visual Basic, add a reference to the COM+ Admin Type Library.</p>
<pre lang="VB"><code>Function SetupMyZoo() As Boolean  ' Return False if any errors occur.

    '  Initialize error handling for this function.
    SetupMyZoo = False 
    On Error GoTo My_Error_Handler

    '  Open a session with the catalog.
    '  Instantiate a COMAdminCatalog object. 
    Dim objCatalog As COMAdminCatalog
    Set objCatalog = CreateObject(&quot;COMAdmin.COMAdminCatalog&quot;)

    '  Create a new COM+ application.
    '  First get the &quot;Applications&quot; collection from the catalog.
    Dim objApplicationsColl As COMAdminCatalogCollection
    Set objApplicationsColl = objCatalog.GetCollection(&quot;Applications&quot;)

    '  Add a new item to this collection. 
    Dim objZooApp As COMAdminCatalogObject
    Set objZooApp = objApplicationsColl.Add

    '  The &quot;Applications&quot; collection determines the available properties.
    '  Set the &quot;Name&quot; property of the new application item. 
    objZooApp.Value(&quot;Name&quot;) = &quot;MyHomeZoo&quot;

    '  Set the &quot;Description&quot; property of the new application item. 
    objZooApp.Value(&quot;Description&quot;) = &quot;My pets at home&quot;

    '  Save changes made to the &quot;Applications&quot; collection. 
    objApplicationsColl.SaveChanges

    '  Install components into the application.
    '  Use the InstallComponent method on COMAdminCatalog. 
    '  In this case, the last two parameters are passed as empty strings.
    objCatalog.InstallComponent &quot;MyHomeZoo&quot;,&quot;MyZoo.DLL&quot;,&quot;&quot;,&quot;&quot; 

    '  Define the roles ZooKeeper and AllergicToCats 
    '  by adding them to the &quot;Roles&quot; collection related to MyHomeZoo. 
    '  First get the &quot;Roles&quot; collection related to MyHomeZoo.
    '  Use the GetCollection method on COMAdminCatalogCollection,
    '  passing in the name of the desired collection, &quot;Roles&quot;, 
    '  and the Key property value of objZooApp.   
    '  The Key property uniquely identifies the object, serving
    '  here to distinguish the &quot;Roles&quot; collection related 
    '  to MyHomeZoo from that of any other application. 
    Dim objRolesColl As COMAdminCatalogCollection
    Set objRolesColl = objApplicationsColl.GetCollection(&quot;Roles&quot;, objZooApp.Key)

    '  Add new items to this &quot;Roles&quot; collection. 
    Dim objZooKeeperRole As COMAdminCatalogObject
    Dim objAllergicToCatsRole As COMAdminCatalogObject
    Set objZooKeeperRole = objRolesColl.Add
    Set objAllergicToCatsRole = objRolesColl.Add

    '  Set the &quot;Name&quot; for the new items.
    objZooKeeperRole.Value(&quot;Name&quot;) = &quot;ZooKeeper&quot; 
    objAllergicToCatsRole.Value(&quot;Name&quot;) = &quot;AllergicToCats&quot; 

    '  Save changes made to any items in this &quot;Roles&quot; collection. 
    objRolesColl.SaveChanges

    '  Assign the AllergicToCats role to the Dog component to 
    '  restrict its scope of access. (The ZooKeeper role, if assigned
    '  only at the application level, can access the whole application.)
    '  First get the &quot;Components&quot; collection related to MyHomeZoo.
    '  Use the GetCollection method on COMAdminCatalogCollection,
    '  passing in the name of the desired collection, &quot;Components&quot;, and
    '  the Key property value of objZooApp. 
    Dim objZooComponentsColl As COMAdminCatalogCollection
    Set objZooComponentsColl = objApplicationsColl.GetCollection(&quot;Components&quot;, objZooApp.Key) 

    '  Find the Dog component item in this &quot;Components&quot; collection.
    '  First Populate the collection to read in data for all its items. 
    objZooComponentsColl.Populate

    '  Enumerate through the &quot;Components&quot; collection 
    '  until the Dog component item is located. 
    Dim objDog As COMAdminCatalogObject 
    For Each objDog in objZooComponentsColl
        If objDog.Name = &quot;Dog&quot; Then 
            Exit For
        End If
    Next 
    '  Set the role checking property at the component level for Dog.
        objDog.Value(&quot;ComponentAccessChecksEnabled&quot;) = True 

    '  Save these changes.
        objZooComponentsColl.SaveChanges
    '  Get the &quot;RolesForComponent&quot; collection related to the 
    '  Dog component, using the Key property of objDog. 
    Dim objRolesForDogColl As COMAdminCatalogCollection 
    Set objRolesForDogColl = objZooComponentsColl.GetCollection(&quot;RolesForComponent&quot;, objDog.Key) 

    '  Add a new item to this &quot;RolesForComponent&quot; collection. 
    Dim objCatSneezerRole As COMAdminCatalogObject
    Set objCatSneezerRole = objRolesForDogColl.Add

    '  Set the &quot;Name&quot; of the new item to be &quot;AllergicToCats&quot;. 
    objCatSneezerRole.Value(&quot;Name&quot;) = &quot;AllergicToCats&quot; 

    '  Save changes made to this &quot;RolesForComponent&quot; collection. 
    objRolesForDogColl.SaveChanges

    '  Now set properties to enforce role-based security. 
    '  First set role-based security at the application level.
    objZooApp.Value(&quot;ApplicationAccessChecksEnabled&quot;) = True 
    objZooApp.Value(&quot;AccessChecksLevel&quot;) = COMAdminAccessChecksApplicationComponentLevel 

    '  Save these changes.
    objApplicationsColl.SaveChanges

    '  Finally, export the new configured MyHomeZoo application to an 
    '  MSI file, used to install the application on other machines.
    '  Use the ExportApplication method on COMAdminCatalogObject.
    objCatalog.ExportApplication &quot;MyHomeZoo&quot;, &quot;c:\Program Files\COM applications\MyHomeZoo.MSI&quot;, 4

    '  Exit the function gracefully.
    SetupMyZoo = True

My_Error_Handler:
    If Not SetupMyZoo Then
        MsgBox &quot;Error # &quot; &amp; Err.Number &amp; &quot; (Hex: &quot; &amp; Hex(Err.Number) &amp; &quot;)&quot; &amp; vbNewLine &amp; Err.Description
    End If
    objCatSneezerRole = Nothing
    objRolesForDogColl = Nothing
    objDog = Nothing
    objZooComponentsColl = Nothing
    objAllergicToCatsRole = Nothing
    objZooKeeperRole = Nothing
    objRolesColl = Nothing
    objZooApp = Nothing
    objApplicationsColl = Nothing
    objCatalog = Nothing
Exit Function
</code></pre>
<h2>Related topics</h2>
<!-- raw HTML omitted -->
<p><a href="com--administration-operations-within-transactions.html">COM+ Administration Operations Within Transactions</a></p>
<!-- raw HTML omitted -->
<p><a href="handling-com--administration-errors.html">Handling COM+ Administration Errors</a></p>
<!-- raw HTML omitted -->
<p><a href="overview-of-the-comadmin-objects.html">Overview of the COMAdmin Objects</a></p>
<!-- raw HTML omitted -->
<p><a href="retrieving-collections-on-the-com--catalog.html">Retrieving Collections on the COM+ Catalog</a></p>
<!-- raw HTML omitted -->
<p><a href="setting-properties-and-saving-changes-to-the-com--catalog.html">Setting Properties and Saving Changes to the COM+ Catalog</a></p>
<!-- raw HTML omitted -->
<p>Â </p>
<p>Â </p>
</body>
