<!DOCTYPE html>
<html lang="en">
<head>
<meta charset="utf-8">
<title>Using the Input-Assembler Stage without Buffers</title>
<style>
table th { border: 1px solid; }
table td { border: 1px solid; }
</style>
</head>
<body>
<hr />
<h2>title: Using the Input-Assembler Stage without Buffers
description: Creating and binding buffers is not necessary if your shaders don't require buffers. This section contains an example of simple vertex and pixel shaders that draw a single triangle.
ms.assetid: 84d24494-f2cb-4ca1-84fd-635e20f2c9ad
ms.topic: article
ms.date: 05/31/2018</h2>
<h1>Using the Input-Assembler Stage without Buffers</h1>
<p>Creating and binding buffers is not necessary if your shaders don't require buffers. This section contains an example of simple vertex and pixel shaders that draw a single triangle.</p>
<ul>
<li><a href="#vertex-shader">Vertex Shader</a></li>
<li><a href="#pixel-shader">Pixel Shader</a></li>
<li><a href="#technique">Technique</a></li>
<li><a href="#application-code">Application Code</a></li>
<li><a href="#related-topics">Related topics</a></li>
</ul>
<h2>Vertex Shader</h2>
<p>For example, you could declare a vertex shader that creates its own vertices.</p>
<pre><code>struct VSIn
{
    uint vertexId : SV_VertexID;
};

struct VSOut
{
    float4 pos : SV_Position;
    float4 color : color;
};

VSOut VSmain(VSIn input)
{
    VSOut output;
    
    if (input.vertexId == 0)
        output.pos = float4(0.0, 0.5, 0.5, 1.0);
    else if (input.vertexId == 2)
        output.pos = float4(0.5, -0.5, 0.5, 1.0);
    else if (input.vertexId == 1)
        output.pos = float4(-0.5, -0.5, 0.5, 1.0);
    
    output.color = clamp(output.pos, 0, 1);
    
    return output;
}
</code></pre>
<h2>Pixel Shader</h2>
<pre><code>// NoBuffer.fx
// Copyright (c) 2004 Microsoft Corporation. All rights reserved.
//

struct PSIn
{
    float4 pos : SV_Position;
    linear float4 color : color;
};

struct PSOut
{
    float4 color : SV_Target;
};


PSOut PSmain(PSIn input)
{
    PSOut output;
    
    output.color = input.color;
    
    return output;
}
</code></pre>
<h2>Technique</h2>
<p>A technique is a collection of rendering passes (there must be at least one pass).</p>
<pre><code>VertexShader vsCompiled = CompileShader( vs_4_0, VSmain() );

technique10 t0
{
    pass p0
    {
        SetVertexShader( vsCompiled );
        SetGeometryShader( NULL );
        SetPixelShader( CompileShader( ps_4_0, PSmain() ));
    }  
}
</code></pre>
<h2>Application Code</h2>
<pre><code>m_pD3D11Device-&gt;IASetInputLayout( NULL );

m_pD3D11Device-&gt;IASetPrimitiveTopology( D3D_PRIMITIVE_TOPOLOGY_TRIANGLELIST );
    
ID3DX11EffectTechnique * pTech = NULL;
pTech = m_pEffect-&gt;GetTechniqueByIndex(0);
pTech-&gt;GetPassByIndex(iPass)-&gt;Apply(0);

m_pD3D11Device-&gt;Draw( 3, 0 );
</code></pre>
<h2>Related topics</h2>
<!-- raw HTML omitted -->
<p><a href="d3d10-graphics-programming-guide-input-assembler-stage-getting-started.html">Getting Started with the Input-Assembler Stage</a></p>
<!-- raw HTML omitted -->
<p>Â </p>
<p>Â </p>
</body>
