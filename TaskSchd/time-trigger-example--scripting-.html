<!DOCTYPE html>
<html lang="en">
<head>
<meta charset="utf-8">
<title>Time Trigger Example (Scripting)</title>
<style>
table th { border: 1px solid; }
table td { border: 1px solid; }
</style>
</head>
<body>
<hr />
<p>title: Time Trigger Example (Scripting)
description: This scripting example shows how to create a task that runs Notepad at a specific time.
ms.assetid: 8511ffcd-166f-4c63-9cd2-ead53dde9ed8
ms.topic: article
ms.date: 05/31/2018
topic_type:</p>
<ul>
<li>kbArticle
api_name:
api_type:
api_location:</li>
</ul>
<hr />
<h1>Time Trigger Example (Scripting)</h1>
<p>This scripting example shows how to create a task that runs Notepad at a specific time. The task contains a time-based trigger that specifies a start boundary to activate the task, an executable action that runs Notepad, and an end boundary that deactivates the task.</p>
<p>The following procedure describes how to schedule a task to start an executable at a specific time.</p>
<p><strong>To Schedule Notepad to start at a Specific Time</strong></p>
<ol>
<li>Create a <a href="taskservice.html"><strong>TaskService</strong></a> object. This object allows you to create the task in a specified folder.</li>
<li>Get a task folder and create a task. Use the <a href="taskservice-getfolder.html"><strong>TaskService.GetFolder</strong></a> method to get the folder where the task is stored and the <a href="taskservice-newtask.html"><strong>TaskService.NewTask</strong></a> method to create the <a href="taskdefinition.html"><strong>TaskDefinition</strong></a> object that represents the task.</li>
<li>Define information about the task using the <a href="taskdefinition.html"><strong>TaskDefinition</strong></a> object. Use the <a href="taskdefinition-settings.html"><strong>TaskDefinition.Settings</strong></a> property to define the settings that determine how the Task Scheduler service performs the task and the <a href="taskdefinition-registrationinfo.html"><strong>TaskDefinition.RegistrationInfo</strong></a> property to define the information that describes the task.</li>
<li>Create a time-based trigger using the <a href="taskdefinition-triggers.html"><strong>TaskDefinition.Triggers</strong></a> property. This property provides access to the <a href="triggercollection.html"><strong>TriggerCollection</strong></a> object. Use the <a href="triggercollection-create.html"><strong>TriggerCollection.Create</strong></a> method (specifying the type of trigger you want to create) to create a time-based trigger. As you create the trigger, set the start boundary and end boundary of the trigger to activate and deactivate the trigger. The start boundary specifies when the task's action will be performed.</li>
<li>Create an action for the task to execute by using the <a href="taskdefinition-actions.html"><strong>TaskDefinition.Actions</strong></a> property. This property provides access to the <a href="actioncollection.html"><strong>ActionCollection</strong></a> object. Use the <a href="actioncollection-create.html"><strong>ActionCollection.Create</strong></a> method to specify the type of action you want to create. This example uses an <a href="execaction.html"><strong>ExecAction</strong></a> object, which represents an action that executes a command-line operation.</li>
<li>Register the task using the <a href="taskfolder-registertaskdefinition.html"><strong>TaskFolder.RegisterTaskDefinition</strong></a> method. For this example the task will start Notepad at the current time plus 30 seconds.</li>
</ol>
<p>The following VBScript example shows how to schedule a task to execute Notepad 30 seconds after the task is registered.</p>
<pre lang="VB"><code>'------------------------------------------------------------------
' This sample schedules a task to start notepad.exe 30 seconds
' from the time the task is registered.
'------------------------------------------------------------------

' A constant that specifies a time-based trigger.
const TriggerTypeTime = 1
' A constant that specifies an executable action.
const ActionTypeExec = 0   


'********************************************************
' Create the TaskService object.
Set service = CreateObject(&quot;Schedule.Service&quot;)
call service.Connect()

'********************************************************
' Get a folder to create a task definition in. 
Dim rootFolder
Set rootFolder = service.GetFolder(&quot;\&quot;)

' The taskDefinition variable is the TaskDefinition object.
Dim taskDefinition
' The flags parameter is 0 because it is not supported.
Set taskDefinition = service.NewTask(0) 

'********************************************************
' Define information about the task.

' Set the registration info for the task by 
' creating the RegistrationInfo object.
Dim regInfo
Set regInfo = taskDefinition.RegistrationInfo
regInfo.Description = &quot;Start notepad at a certain time&quot;
regInfo.Author = &quot;Author Name&quot;

'********************************************************
' Set the principal for the task
Dim principal
Set principal = taskDefinition.Principal

' Set the logon type to interactive logon
principal.LogonType = 3


' Set the task setting info for the Task Scheduler by
' creating a TaskSettings object.
Dim settings
Set settings = taskDefinition.Settings
settings.Enabled = True
settings.StartWhenAvailable = True
settings.Hidden = False

'********************************************************
' Create a time-based trigger.
Dim triggers
Set triggers = taskDefinition.Triggers

Dim trigger
Set trigger = triggers.Create(TriggerTypeTime)

' Trigger variables that define when the trigger is active.
Dim startTime, endTime

Dim time
time = DateAdd(&quot;s&quot;, 30, Now)  'start time = 30 seconds from now
startTime = XmlTime(time)

time = DateAdd(&quot;n&quot;, 5, Now) 'end time = 5 minutes from now
endTime = XmlTime(time)

WScript.Echo &quot;startTime :&quot; &amp; startTime
WScript.Echo &quot;endTime :&quot; &amp; endTime

trigger.StartBoundary = startTime
trigger.EndBoundary = endTime
trigger.ExecutionTimeLimit = &quot;PT5M&quot;    'Five minutes
trigger.Id = &quot;TimeTriggerId&quot;
trigger.Enabled = True

'***********************************************************
' Create the action for the task to execute.

' Add an action to the task to run notepad.exe.
Dim Action
Set Action = taskDefinition.Actions.Create( ActionTypeExec )
Action.Path = &quot;C:\Windows\System32\notepad.exe&quot;

WScript.Echo &quot;Task definition created. About to submit the task...&quot;

'***********************************************************
' Register (create) the task.

call rootFolder.RegisterTaskDefinition( _
    &quot;Test TimeTrigger&quot;, taskDefinition, 6, , , 3)

WScript.Echo &quot;Task submitted.&quot;



'------------------------------------------------------------------
' Used to get the time for the trigger 
' startBoundary and endBoundary.
' Return the time in the correct format: 
' YYYY-MM-DDTHH:MM:SS. 
'------------------------------------------------------------------
Function XmlTime(t)
    Dim cSecond, cMinute, CHour, cDay, cMonth, cYear
    Dim tTime, tDate

    cSecond = &quot;0&quot; &amp; Second(t)
    cMinute = &quot;0&quot; &amp; Minute(t)
    cHour = &quot;0&quot; &amp; Hour(t)
    cDay = &quot;0&quot; &amp; Day(t)
    cMonth = &quot;0&quot; &amp; Month(t)
    cYear = Year(t)

    tTime = Right(cHour, 2) &amp; &quot;:&quot; &amp; Right(cMinute, 2) &amp; _
        &quot;:&quot; &amp; Right(cSecond, 2)
    tDate = cYear &amp; &quot;-&quot; &amp; Right(cMonth, 2) &amp; &quot;-&quot; &amp; Right(cDay, 2)
    XmlTime = tDate &amp; &quot;T&quot; &amp; tTime 
End Function

</code></pre>
<h2>Related topics</h2>
<!-- raw HTML omitted -->
<p><a href="using-the-task-scheduler.html">Using the Task Scheduler</a></p>
<!-- raw HTML omitted -->
<p>Â </p>
<p>Â </p>
</body>
