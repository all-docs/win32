<!DOCTYPE html>
<html lang="en">
<head>
<meta charset="utf-8">
<title>Listing Profiles, Policies, and Clients</title>
<style>
table th { border: 1px solid; }
table td { border: 1px solid; }
</style>
</head>
<body>
<hr />
<h2>title: Listing Profiles, Policies, and Clients
description: Listing Profiles, Policies, and Clients
ms.assetid: 8bfbe4f6-b098-43c8-acb7-2c489ebe0ad3
ms.tgt_platform: multiple
ms.topic: article
ms.date: 05/31/2018</h2>
<h1>Listing Profiles, Policies, and Clients</h1>
<p>The following VBScript code enumerates the profiles, policies, and clients on the local computer.</p>
<pre lang="VB"><code>Option Explicit

Const DATA_STORE_LOCAL = 0
Const PROPERTY_SDO_NAME = 2

Const PROPERTY_IAS_POLICIES_COLLECTION = 1025
Const PROPERTY_IAS_PROFILES_COLLECTION = 1026

Const PROPERTY_IAS_PROTOCOLS_COLLECTION = 1027

Const PROPERTY_RADIUS_CLIENTS_COLLECTION = 1029

Const PROPERTY_CLIENT_REQUIRE_SIGNATURE = 1024 
Const PROPERTY_CLIENT_SHARED_SECRET = 1026 
Const PROPERTY_CLIENT_NAS_MANUFACTURER = 1027 
Const PROPERTY_CLIENT_ADDRESS = 1028 


Const PROPERTY_PROFILE_ATTRIBUTES_COLLECTION = 1024
Const PROPERTY_ATTRIBUTE_ID = 1024
Const PROPERTY_ATTRIBUTE_DISPLAY_NAME = 1035

Const PROPERTY_POLICY_CONSTRAINT = 1024
Const PROPERTY_POLICY_MERIT = 1025 
Const PROPERTY_POLICY_PROFILE_NAME = 1028 
Const PROPERTY_POLICY_ACTION = 1029
Const PROPERTY_POLICY_CONDITIONS_COLLECTION = 1030

Const PROPERTY_CONDITION_TEXT = 1024


Dim sdoMachine, sdoServiceSDO
Dim sdoCollProfiles, sdoCollProtocols, sdoCollClients, sdoCollPolicies
Dim sdoCollAttributes, sdoCollConditions
Dim sdoRADIUS
Dim vtProfile, vtProtocol, vtClient, vtPolicy, vtAttribute, vtCondition

Set sdoMachine = CreateObject(&quot;IAS.SdoMachine&quot;)
sdoMachine.Attach vbNullString
Set sdoServiceSDO = sdoMachine.GetServiceSDO(DATA_STORE_LOCAL, &quot;IAS&quot;)

'
' List Profiles and Attributes
'
Set sdoCollProfiles = sdoServiceSDO.GetProperty(PROPERTY_IAS_PROFILES_COLLECTION)

WScript.Echo sdoCollProfiles.Count &amp; &quot; Profiles found.&quot;
For Each vtProfile in sdoCollProfiles
    WScript.Echo &quot;- &quot; &amp; Chr(34) &amp; vtProfile.GetProperty(PROPERTY_SDO_NAME) &amp; Chr(34)
    set sdoCollAttributes = vtProfile.GetProperty(PROPERTY_PROFILE_ATTRIBUTES_COLLECTION)
    WScript.Echo sdoCollAttributes.Count &amp; &quot; Attributes found.&quot;
    For Each vtAttribute in sdoCollAttributes
        WScript.Echo &quot; - &quot; &amp; Chr(34) &amp; vtAttribute.GetProperty(PROPERTY_SDO_NAME) &amp; Chr(34)
        WScript.Echo &quot;   LDAPName &quot; &amp; Chr(34) &amp; vtAttribute.GetProperty(PROPERTY_ATTRIBUTE_DISPLAY_NAME) &amp; Chr(34)
        WScript.Echo &quot;   Id &quot; &amp; Chr(34) &amp; vtAttribute.GetProperty(PROPERTY_ATTRIBUTE_ID) &amp; Chr(34)
    Next 'vtAttribute
Next 'vtProfile

'
' List Policies and Conditions
'
Set sdoCollPolicies = sdoServiceSDO.GetProperty(PROPERTY_IAS_POLICIES_COLLECTION)

WScript.Echo sdoCollPolicies.Count &amp; &quot; Policies found.&quot;
For Each vtPolicy in sdoCollPolicies
    WScript.Echo &quot;- &quot; &amp; Chr(34) &amp; vtPolicy.GetProperty(PROPERTY_SDO_NAME) &amp; Chr(34)

    WScript.Echo &quot;  Merit &quot; &amp; Chr(34) &amp; vtPolicy.GetProperty(PROPERTY_POLICY_MERIT) &amp; Chr(34)
    WScript.Echo &quot;  Policy Name &quot; &amp; Chr(34) &amp; vtPolicy.GetProperty(PROPERTY_POLICY_PROFILE_NAME) &amp; Chr(34)

    set sdoCollConditions = vtPolicy.GetProperty(PROPERTY_POLICY_CONDITIONS_COLLECTION)
    WScript.Echo sdoCollConditions.Count &amp; &quot; Conditions found.&quot;
    For Each vtCondition in sdoCollConditions
        WScript.Echo &quot; - &quot; &amp; Chr(34) &amp; vtCondition.GetProperty(PROPERTY_SDO_NAME) &amp; Chr(34)
        WScript.Echo &quot;   Text &quot; &amp; Chr(34) &amp; vtCondition.GetProperty(PROPERTY_CONDITION_TEXT) &amp; Chr(34)
    Next 'vtCondition
Next 'vtPolicy 

'
' List Clients
'
Set sdoCollProtocols = sdoServiceSDO.GetProperty(PROPERTY_IAS_PROTOCOLS_COLLECTION)
Set sdoRADIUS = sdoCollProtocols.Item(&quot;Microsoft Radius Protocol&quot;)
Set sdoCollClients = sdoRADIUS.GetProperty(PROPERTY_RADIUS_CLIENTS_COLLECTION)

WScript.Echo 
WScript.Echo sdoCollClients.Count &amp; &quot; Clients found.&quot;
For Each vtClient in sdoCollClients
    WScript.Echo &quot;- &quot; &amp; Chr(34) &amp; vtClient.GetProperty(PROPERTY_SDO_NAME) &amp; Chr(34)
    WScript.Echo &quot; - Require Signature &quot; &amp; Chr(34) &amp; vtClient.GetProperty(PROPERTY_CLIENT_REQUIRE_SIGNATURE) &amp; Chr(34)
    WScript.Echo &quot; - Shared Secret &quot; &amp; Chr(34) &amp; vtClient.GetProperty(PROPERTY_CLIENT_SHARED_SECRET) &amp; Chr(34)
    WScript.Echo &quot; - NAS Manufacturer&quot; &amp; Chr(34) &amp; vtClient.GetProperty(PROPERTY_CLIENT_NAS_MANUFACTURER) &amp; Chr(34)
    WScript.Echo &quot; - Address&quot; &amp; Chr(34) &amp; vtClient.GetProperty(PROPERTY_CLIENT_ADDRESS) &amp; Chr(34)
Next 'vtClient
</code></pre>
<h2>Related topics</h2>
<!-- raw HTML omitted -->
<p><a href="/windows/desktop/api/sdoias/ne-sdoias-clientproperties"><strong>CLIENTPROPERTIES</strong></a></p>
<!-- raw HTML omitted -->
<p><a href="/windows/desktop/Nps/sdo-enumerating-objects-in-a-collection">Enumerating Objects in a Collection</a></p>
<!-- raw HTML omitted -->
<p><a href="/windows/desktop/api/sdoias/ne-sdoias-iascommonproperties"><strong>IASCOMMONPROPERTIES</strong></a></p>
<!-- raw HTML omitted -->
<p><a href="/windows/desktop/api/sdoias/ne-sdoias-iasproperties"><strong>IASPROPERTIES</strong></a></p>
<!-- raw HTML omitted -->
<p><a href="/windows/desktop/api/sdoias/nn-sdoias-isdo"><strong>ISdo</strong></a></p>
<!-- raw HTML omitted -->
<p><a href="/windows/desktop/api/sdoias/nn-sdoias-isdomachine"><strong>ISdoMachine</strong></a></p>
<!-- raw HTML omitted -->
<p><a href="/windows/desktop/api/sdoias/ne-sdoias-radiusproperties"><strong>RADIUSPROPERTIES</strong></a></p>
<!-- raw HTML omitted -->
<p>Â </p>
<p>Â </p>
</body>
