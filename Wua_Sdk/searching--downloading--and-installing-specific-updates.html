<!DOCTYPE html>
<html lang="en">
<head>
<meta charset="utf-8">
<title>Searching, Downloading, and Installing Specific Updates</title>
<style>
table th { border: 1px solid; }
table td { border: 1px solid; }
</style>
</head>
<body>
<hr />
<h2>description: The scripting sample in this topic shows you how to use the Windows Update Agent (WUA) to scan, download, and install a specific update. The update can be specified by its title.
ms.assetid: 4a5bb920-fc51-48a0-8f66-bb2fcc72589f
title: Searching, Downloading, and Installing Specific Updates
ms.topic: article
ms.date: 05/31/2018</h2>
<h1>Searching, Downloading, and Installing Specific Updates</h1>
<p>The scripting sample in this topic shows you how to use the Windows Update Agent (WUA) to scan, download, and install a specific update. The update can be specified by its title.</p>
<p>The sample searches for a specific software update, downloads the update, and then installs the update. For example, a user can use this method to determine if a critical security update is installed on a computer. If the update isn't installed, the user can ensure that the update is downloaded and installed. The user can also ensure that they are notified about the status of the installation.</p>
<p>The sample update is identified by the update title in <a href="/windows/desktop/api/Wuapi/nf-wuapi-iupdate-get_title"><strong>Title Property of IUpdate</strong></a>. The title of the update that is suggested in this sample is &quot;Update for Windows Rights Management client 1.0.&quot;</p>
<blockquote>
<p>[!Note]<br />
For info about how to search, download, and install all the updates that apply to a specific application, see <a href="searching--downloading--and-installing-updates.html">Searching, Downloading, and Installing Updates</a>.</p>
</blockquote>
<p>Â </p>
<p>Before you attempt to run this sample, note the following:</p>
<ul>
<li>WUA must be installed on the computer. For more info about how to determine the version of WUA that is installed, see <a href="determining-the-current-version-of-wua.html">Determining the Current Version of WUA</a>.</li>
<li>The sample doesn't provide its own user interface. WUA prompts the user to restart the computer if an update requires a restart.</li>
<li>The sample can download updates only from WUA. It can't download updates from a Software Update Services (SUS) 1.0 server.</li>
<li>Running this sample requires Windows Script Host (WSH). For more info about WSH, see the WSH section of the Platform Software Development Kit (SDK). If the sample is copied to a file named WUA_SpecificUpdate.vbs, you can run it by opening a Command Prompt window and by typing this command: <strong>cscript WUA_SpecificUpdate.vbs</strong></li>
</ul>
<blockquote>
<p>[!NOTE]
When conducting scans, you may experience higher than normal memory usage. It is recommended to make necessary adjustments to your system to allocate sufficient memory resources for the scanning process. This may include configuring additional processors and modifying the pagefile.  Ensuring adequate memory allocation will help in completing the scan efficiently and effectively.</p>
</blockquote>
<h2>Example</h2>
<blockquote>
<p>[!IMPORTANT]
This script is intended to demonstrate the use of the Windows Update Agent APIs, and provide an example of how developers can use these APIs to solve problems. This script is not intended as production code, and the script itself is not supported by Microsoft (though the underlying Windows Update Agent APIs are supported).</p>
</blockquote>
<p>Â </p>
<pre lang="VB"><code>Set updateSession = CreateObject(&quot;Microsoft.Update.Session&quot;)
updateSession.ClientApplicationID = &quot;Sample Script&quot;

'Get update title to search for
WScript.Echo &quot;Enter the title of the update: &quot; &amp; _
&quot;(for example, Update for Windows Rights Management client 1.0)&quot;
updateTitle = WScript.StdIn.Readline

WScript.Echo vbCRLF &amp; &quot;Searching for: &quot; &amp; updateTitle &amp; &quot;...&quot;

Set updateSearcher = updateSession.CreateupdateSearcher()

'Search for all software updates, already installed and not installed
Set searchResult = _
updateSearcher.Search(&quot;Type='Software'&quot;)

Set updateToInstall = CreateObject(&quot;Microsoft.Update.UpdateColl&quot;)

updateIsApplicable = False

'Cycle through search results to look for the update title
For i = 0 To searchResult.Updates.Count-1
   Set update = searchResult.Updates.Item(i)
   If UCase(update.Title) = UCase(updateTitle) Then
   'Update in list of applicable updates 
   'Determine if it is already installed or not
      If update.IsInstalled = False Then
         WScript.Echo vbCRLF &amp; _
         &quot;Result: Update applicable, not installed.&quot;
         updateIsApplicable = True
         updateToInstall.Add(update)
      Else 
         'Update is installed so notify user and quit
         WScript.Echo vbCRLF &amp; _
         &quot;Result: Update applicable, already installed.&quot;
         updateIsApplicable = True
         WScript.Quit 
      End If
   End If 
Next

If updateIsApplicable = False Then
   WScript.Echo &quot;Result: Update is not applicable to this machine.&quot;
   WScript.Quit
End If

WScript.Echo vbCRLF &amp; &quot;Would you like to install now? (Y/N)&quot;
stdInput = WScript.StdIn.Readline
 
If (strInput = &quot;N&quot; or strInput = &quot;n&quot;) Then 
   WScript.Quit
ElseIf  (stdInput = &quot;Y&quot; OR stdInput = &quot;y&quot;) Then
   'Download update
   Set downloader = updateSession.CreateUpdateDownloader() 
   downloader.Updates = updateToInstall
   WScript.Echo vbCRLF &amp; &quot;Downloading...&quot;
   Set downloadResult = downloader.Download()
   WScript.Echo &quot;Download Result: &quot; &amp; downloadResult.ResultCode
  
   'Install Update
   Set installer = updateSession.CreateUpdateInstaller()
   WScript.Echo vbCRLF &amp; &quot;Installing...&quot;
   installer.Updates = updateToInstall
   Set installationResult = installer.Install()
  
   'Output the result of the installation
   WScript.Echo &quot;Installation Result: &quot; &amp; _
   installationResult.ResultCode
   WScript.Echo &quot;Reboot Required: &quot; &amp; _
   installationResult.RebootRequired 
End If
</code></pre>
<p>Â </p>
<p>Â </p>
</body>
