<!DOCTYPE html>
<html lang="en">
<head>
<meta charset="utf-8">
<title>Searching, Downloading, and Installing Updates</title>
<style>
table th { border: 1px solid; }
table td { border: 1px solid; }
</style>
</head>
<body>
<hr />
<h2>description: The scripting sample in this topic shows you how to use Windows Update Agent (WUA) to scan, download, and install updates.
ms.assetid: 4b2b1898-64f1-4908-98b7-ea87a6fcb71d
title: Searching, Downloading, and Installing Updates
ms.topic: article
ms.date: 01/16/2020</h2>
<h1>Searching, Downloading, and Installing Updates</h1>
<p>The scripting sample in this topic shows you how to use Windows Update Agent (WUA) to scan, download, and install updates.</p>
<p>The sample searches for all the applicable software updates and then lists those updates. Next, it creates a collection of updates to download and then downloads them. Finally, it creates a collection of updates to install and then installs them.</p>
<p>If you want to search, download, and install a specific update that you identify by using the update title, see <a href="searching--downloading--and-installing-specific-updates.html">Searching, Downloading, and Installing Specific Updates</a>.</p>
<p>Before you attempt to run this sample, note the following:</p>
<ul>
<li>
<p>WUA must be installed on the computer. For more information about how to determine the version of WUA that is installed, see <a href="determining-the-current-version-of-wua.html">Determining the Current Version of WUA</a>.</p>
</li>
<li>
<p>Running this sample requires Windows Script Host (WSH). For more information about WSH, see the WSH section of the Platform Software Development Kit (SDK). If the sample is copied to a file named WUA_SearchDownloadInstall.vbs, you can run the sample by opening a Command Prompt window and typing the following command at the command prompt.</p>
<p><strong>cscript WUA_SearchDownloadInstall.vbs</strong></p>
</li>
</ul>
<blockquote>
<p>[!NOTE]
When conducting scans, you may experience higher than normal memory usage. It is recommended to make necessary adjustments to your system to allocate sufficient memory resources for the scanning process. This may include configuring additional processors and modifying the pagefile.  Ensuring adequate memory allocation will help in completing the scan efficiently and effectively.</p>
</blockquote>
<h2>Example</h2>
<blockquote>
<p>[!IMPORTANT]
This script is intended to demonstrate the use of the Windows Update Agent APIs, and provide an example of how developers can use these APIs to solve problems. This script is not intended as production code, and the script itself is not supported by Microsoft (though the underlying Windows Update Agent APIs are supported).</p>
</blockquote>
<p>Â </p>
<pre lang="VB"><code>Option Explicit

WScript.Echo &quot;This script is provided by Microsoft Corporation to demonstrate techniques that can be used to search, download,&quot;
WScript.Echo &quot;and install updates through the Windows Update Agent API.&quot;
WScript.Echo &quot;&quot;
WScript.Echo &quot;This script is not intended as production code.&quot;
WScript.Echo &quot;&quot;

' Supported parameters:
'    /AppName: Name to pass to the WUA API as the 'calling application';
'             this appears in the Windows Update logs
'             Default: &quot;WUA API Sample Script&quot;
'    /Criteria: Search criteria to be used (see the WUA documentation for
'             IUpdateSearcher::BeginSearch for examples)
'             Default: Search for all non-installed, non-hidden software
'    /Automate: Don't prompt the user for various actions
'             Default: Prompt the user
'    /IgnoreSupersedence: Display all applicable updates, even those
'             superseded by newer updates
'             Default: Ignore supersedes updates
'    /Offline: Path to WSUSSCN2.cab file that should be used for offline sync
'             Default: Don't do offline sync
'    /Service: Update service that script should scan against
'             Available values: WU, MU, WSUS, DCAT, STORE, or an
'             identifying GUID of another service
'             Default: Default service (WSUS if machine is configured to
'                 use it, or MU if opted in, or WU otherwise)
'             Ignored if /Offline is set
'    /Hide: Hide updates found by the scan. Hidden updates will not
'             normally be installed by Automatic Updates.
'    /Show: Unhide any hidden updates found by the scan.
'    /NoDownload: Do not download any updates that the scan detects
'    /NoInstall: Do not install any updates that the scan detects
'    /ShowDetails: Show details about the updates found by the scan
'    /ShowBundle: Output information about the child updates in the 
'             bundled updates that are found
'    /RebootToComplete: Restart the computer if necessary to complete
'             installation

Function InstallationResultToText(result)
    Select Case result
        Case 2
            InstallationResultToText = &quot;Succeeded&quot;
        Case 3
            InstallationResultToText = &quot;Succeeded with errors&quot;
        Case 4
            InstallationResultToText = &quot;Failed&quot;
        Case 5
            InstallationResultToText = &quot;Cancelled&quot;
        Case Else
            InstallationResultToText = &quot;Unexpected (&quot; &amp; result &amp; &quot;)&quot;
    End Select
End Function

Function DeploymentActionToText(action)
    Select Case action
        Case 0
            DeploymentActionToText = &quot;None (Inherit)&quot;
        Case 1
            DeploymentActionToText = &quot;Installation&quot;
        Case 2
            DeploymentActionToText = &quot;Uninstallation&quot;
        Case 3
            DeploymentActionToText = &quot;Detection&quot;
        Case 4
            DeploymentActionToText = &quot;Optional Installation&quot;
        Case Else
            DeploymentActionToText = &quot;Unexpected (&quot; &amp; action &amp; &quot;)&quot;
    End Select
End Function


Function UpdateDescription(update)
    Dim description
    Dim I
    Dim category
    description = update.Title &amp; &quot; {&quot; &amp; update.Identity.UpdateID &amp; &quot;.&quot; &amp; update.Identity.RevisionNumber &amp; &quot;}&quot;
    If update.IsHidden Then
        description = description &amp; &quot; (hidden)&quot;
    End If
    If WScript.Arguments.Named.Exists(&quot;ShowDetails&quot;) Then
        if update.KBArticleIDs.Count &gt; 0 Then
            description = description &amp; &quot; (&quot;
            For I = 0 To update.KBArticleIDs.Count -1
                If I &gt; 0 Then
                    description = description &amp; &quot;,&quot;
                End If
                description = description &amp; &quot;KB&quot; &amp; update.KBArticleIDs.Item(I)
            Next
            description = description &amp; &quot;)&quot;
        End If
        description = description &amp; &quot; Categories: &quot;
        For I = 0 to update.Categories.Count - 1
            Set category = update.Categories.Item(I)
            If I &gt; 0 Then
                description = description &amp; &quot;,&quot;
            End If
            description = description &amp; category.Name &amp; &quot; {&quot; &amp; category.CategoryID &amp; &quot;}&quot;
        Next
        description = description &amp; &quot; Deployment action: &quot; &amp; DeploymentActionToText(update.DeploymentAction)
    End If
    UpdateDescription = description
End Function

Dim appName
If WScript.Arguments.Named.Exists(&quot;AppName&quot;) Then
    appName = WScript.Arguments.Named.Item(&quot;AppName&quot;)
Else
    appName = &quot;WUA API Sample Script&quot;
End If

Dim criteria
If WScript.Arguments.Named.Exists(&quot;Criteria&quot;) Then
    criteria = WScript.Arguments.Named.Item(&quot;Criteria&quot;)
Else
    criteria = &quot;IsInstalled=0 and Type='Software' and IsHidden=0&quot;
End If

Dim isAutomated
isAutomated = WScript.Arguments.Named.Exists(&quot;Automate&quot;)

Dim ignoreSupersedence
ignoreSupersedence = WScript.Arguments.Named.Exists(&quot;IgnoreSupersedence&quot;)

Dim showBundle
showBundle = WScript.Arguments.Named.Exists(&quot;ShowBundle&quot;)

Dim serverSelection
Dim serviceId
serverSelection = 0
serviceId = &quot;&quot;

If WScript.Arguments.Named.Exists(&quot;Service&quot;) Then
    Select Case UCase(WScript.Arguments.Named.Item(&quot;Service&quot;))
        Case &quot;WU&quot;
            serverSelection = 2
        Case &quot;MU&quot;
            serverSelection = 3
            serviceId = &quot;7971f918-a847-4430-9279-4a52d1efe18d&quot;
        Case &quot;WSUS&quot;
            serverSelection = 1
        case &quot;DCAT&quot;
            serverSelection = 3
            serviceId = &quot;855E8A7C-ECB4-4CA3-B045-1DFA50104289&quot;
        case &quot;STORE&quot;
            serverSelection = 3
            serviceId = &quot;117cab2d-82b1-4b5a-a08c-4d62dbee7782&quot;
        Case Else
            serverSelection = 3
            serviceId = WScript.Arguments.Named.Item(&quot;Service&quot;)
    End Select
End If

Dim noDownload
noDownload = WScript.Arguments.Named.Exists(&quot;NoDownload&quot;)

Dim noInstall
noInstall = WScript.Arguments.Named.Exists(&quot;NoInstall&quot;)

Dim hide
hide = WScript.Arguments.Named.Exists(&quot;Hide&quot;)

Dim show
show = WScript.Arguments.Named.Exists(&quot;Show&quot;)

Dim strInput
strInput = &quot;&quot;

Dim returnValue
returnValue = 0

Dim automaticUpdates
Dim updateSettings
Dim objWMIService
Dim colListOfServices
Dim objService

Dim secondInstallPass
secondInstallPass = false

Dim updateSession
Set updateSession = CreateObject(&quot;Microsoft.Update.Session&quot;)
updateSession.ClientApplicationID = appName

Dim updateServiceManager
Dim offlineService
If WScript.Arguments.Named.Exists(&quot;Offline&quot;) Then
    Set updateServiceManager = CreateObject(&quot;Microsoft.Update.ServiceManager&quot;)
    Set offlineService = updateServiceManager.AddScanPackageService(appName, WScript.Arguments.Named.Item(&quot;Offline&quot;), 0)
    serverSelection = 3
    serviceId = offlineService.ServiceID
    WScript.Echo &quot;Registered offline scan cab, service ID &quot; &amp; serviceId &amp; vbCRLF
End If
    
Dim updateSearcher
Set updateSearcher = updateSession.CreateUpdateSearcher()
updateSearcher.ServerSelection = serverSelection
If serverSelection = 3 Then
    updateSearcher.ServiceId = serviceId
End If
If ignoreSupersedence Then
    updateSearcher.IncludePotentiallySupersededUpdates = true
End If

WScript.Echo &quot;Searching for updates...&quot; &amp; vbCRLF

Dim searchResult
Set searchResult = updateSearcher.Search(criteria)

WScript.Echo &quot;List of applicable items found on the machine:&quot;

Dim I
Dim B
For I = 0 To searchResult.Updates.Count-1
    Set update = searchResult.Updates.Item(I)
    WScript.Echo I + 1 &amp; &quot;&gt; &quot; &amp; UpdateDescription(update)
    If showBundle Then
        For B = 0 to update.BundledUpdates.Count-1
            WScript.Echo I+1 &amp; &quot;&gt; &quot; &amp; B+1 &amp; &quot;&gt; &quot; &amp; UpdateDescription(update.BundledUpdates.Item(B))
        Next
    End If
Next

Dim updatesToDownload
Set updatesToDownload = CreateObject(&quot;Microsoft.Update.UpdateColl&quot;)

If searchResult.Updates.Count = 0 Then
    WScript.Echo &quot;There are no applicable updates.&quot;
Else
    WScript.Echo vbCRLF &amp; &quot;Checking search results:&quot;

    Dim update
    Dim description
    Dim addThisUpdate
    Dim hideThisUpdate
    Dim showThisUpdate
    Dim atLeastOneAdded
    Dim propertyTest
    atLeastOneAdded = false
    Dim exclusiveAdded
    exclusiveAdded = false
    If noDownload And Not hide And Not show Then
        WScript.Echo &quot;Skipping downloads as requested.&quot;
    Else
        For I = 0 to searchResult.Updates.Count-1
            Set update = searchResult.Updates.Item(I)
            description = UpdateDescription(update)
            addThisUpdate = false
            If hide And Not update.IsHidden Then
                strInput = &quot;Y&quot;
                If Not isAutomated Then
                    WScript.Echo I + 1 &amp; &quot;&gt; : &quot; &amp; description  &amp; _
                    &quot; is now shown; do you want to hide it? ([Y]/N)&quot;
                    strInput = WScript.StdIn.Readline
                    WScript.Echo
                End If
                If (strInput = &quot;Y&quot; or strInput = &quot;y&quot;) Then
                    WScript.Echo &quot;Hiding update&quot;
                    update.IsHidden = true
                End If
            ElseIf show And update.IsHidden Then
                strInput = &quot;Y&quot;
                If Not isAutomated Then
                    WScript.Echo I + 1 &amp; &quot;&gt; : &quot; &amp; description  &amp; _
                    &quot; is now hidden; do you want to show it? ([Y]/N)&quot;
                    strInput = WScript.StdIn.Readline
                    WScript.Echo
                End If
                If (strInput = &quot;Y&quot; or strInput = &quot;y&quot;) Then
                    WScript.Echo &quot;Showing update&quot;
                    update.IsHidden = false
                End If
            End If
                
            If exclusiveAdded Then
                WScript.Echo I + 1 &amp; &quot;&gt; skipping: &quot; &amp; description &amp; _
                    &quot; because an exclusive update has already been selected&quot;
            ElseIf Not noDownload Then
                propertyTest = false
                On Error Resume Next
                propertyTest = update.InstallationBehavior.CanRequestUserInput
                On Error GoTo 0
                If propertyTest = true Then
                    WScript.Echo I + 1 &amp; &quot;&gt; skipping: &quot; &amp; description  &amp; _
                    &quot; because it requires user input&quot;
                Else
                    propertyTest = true
                    On Error Resume Next
                    propertyTest = update.EulaAccepted
                    On Error GoTo 0
                    If propertyTest = false Then
                        If isAutomated Then
                            WScript.Echo I + 1 &amp; &quot;&gt; skipping: &quot; &amp; description  &amp; _
                            &quot; because it has a license agreement that has not been accepted&quot;
                        Else
                            WScript.Echo I + 1 &amp; &quot;&gt; note: &quot; &amp; description  &amp; _
                            &quot; has a license agreement that must be accepted:&quot;
                            WScript.Echo update.EulaText
                            WScript.Echo &quot;Do you accept this license agreement? (Y/[N])&quot;
                            strInput = WScript.StdIn.Readline
                            WScript.Echo 
                            If (strInput = &quot;Y&quot; or strInput = &quot;y&quot;) Then
                                update.AcceptEula()
                                addThisUpdate = true
                            Else
                                WScript.Echo I + 1 &amp; &quot;&gt; skipping: &quot; &amp; description  &amp; _
                                &quot; because the license agreement was declined&quot;
                            End If
                        End If
                    Else
                        addThisUpdate = true
                    End If
                End If
                If addThisUpdate Then
                    propertyTest = 0
                    On Error Resume Next
                    propertyTest = update.InstallationBehavior.Impact
                    On Error GoTo 0
                    If propertyTest = 2 Then
                        If atLeastOneAdded Then
                            WScript.Echo I + 1 &amp; &quot;&gt; skipping: &quot; &amp; description  &amp; _
                            &quot; because it is exclusive and other updates are being installed first&quot;
                            addThisUpdate = false
                        End If
                    End If
                End If
                If addThisUpdate Then
                    If Not isAutomated Then
                        WScript.Echo I + 1 &amp; &quot;&gt; : &quot; &amp; description  &amp; _
                        &quot; is applicable; do you want to install it? ([Y]/N)&quot;
                        strInput = WScript.StdIn.Readline
                        WScript.Echo
                        If (strInput = &quot;N&quot; or strInput = &quot;n&quot;) Then
                            WScript.Echo &quot;Skipping update&quot;
                            addThisUpdate = false
                       End If
                    End If
                End If
                If addThisUpdate Then
                    WScript.Echo I + 1 &amp; &quot;&gt; adding: &quot; &amp; description  
                    updatesToDownload.Add(update)
                    atLeastOneAdded = true
                    On Error Resume Next
                    propertyTest = update.InstallationBehavior.Impact
                    On Error GoTo 0
                    If propertyTest = 2 Then
                        WScript.Echo &quot;This update is exclusive; skipping remaining updates&quot;
                        exclusiveAdded = true
                    End If
                End If
            End If
        Next
    End If
End If

Dim updatesToInstall
Set updatesToInstall = CreateObject(&quot;Microsoft.Update.UpdateColl&quot;)

Dim rebootMayBeRequired
rebootMayBeRequired = false

If (Not noDownload) And (updatesToDownload.Count &gt; 0) Then
    WScript.Echo vbCRLF &amp; &quot;Downloading updates...&quot;

    Dim downloader
    Set downloader = updateSession.CreateUpdateDownloader() 
    downloader.Updates = updatesToDownload
    downloader.Download()

    WScript.Echo vbCRLF &amp; &quot;Successfully downloaded updates:&quot;

    For I = 0 To updatesToDownload.Count-1
        Set update = updatesToDownload.Item(I)
        If update.IsDownloaded = true Then
            WScript.Echo I + 1 &amp; &quot;&gt; &quot; &amp; UpdateDescription(update) 
            updatesToInstall.Add(update) 
            propertyTest = 0
            On Error Resume Next
            propertyTest = update.InstallationBehavior.RebootBehavior
            On Error GoTo 0
            If propertyTest &gt; 0 Then
                rebootMayBeRequired = true
            End If
        End If
    Next
End If

strInput = &quot;N&quot;
If noInstall Then
    WScript.Echo &quot;Skipping install as requested.&quot;
Else
    If updatesToInstall.Count &gt; 0 Then
        strInput = &quot;Y&quot;
        If rebootMayBeRequired = true Then
            WScript.Echo vbCRLF &amp; &quot;These updates may require a reboot.&quot;
        End If
        If Not isAutomated Then
            WScript.Echo  vbCRLF &amp; &quot;Would you like to install updates now? (Y/[N])&quot;
            strInput = WScript.StdIn.Readline
            WScript.Echo 
        End If
    End If

    If (strInput = &quot;Y&quot; or strInput = &quot;y&quot;) Then
        WScript.Echo &quot;Installing updates...&quot;
        Dim installer
        Set installer = updateSession.CreateUpdateInstaller()
        installer.Updates = updatesToInstall
        Dim installationResult
        Set installationResult = installer.Install()
 
        returnValue = 1
        'Output results of install
        WScript.Echo &quot;Installation Result: &quot; &amp; _
        InstallationResultToText(installationResult.ResultCode) 
        WScript.Echo &quot;Reboot Required: &quot; &amp; _ 
        installationResult.RebootRequired &amp; vbCRLF 
        WScript.Echo &quot;Listing of updates installed &quot; &amp; _
        &quot;and individual installation results:&quot; 
 
        For I = 0 to updatesToInstall.Count - 1
            WScript.Echo I + 1 &amp; &quot;&gt; &quot; &amp; _
            UpdateDescription(updatesToInstall.Item(i)) &amp; &quot;: &quot; &amp; _
            InstallationResultToText(installationResult.GetUpdateResult(I).ResultCode) &amp; _
            &quot; HRESULT: &quot; &amp; installationResult.GetUpdateResult(I).HResult
            If installationResult.GetUpdateResult(I).HResult = -2145116147 Then
                WScript.Echo &quot;An update needed additional downloaded content. Please rerun the script.&quot;
            End If
        Next
       
        If installationResult.RebootRequired And WScript.Arguments.Named.Exists(&quot;RebootToComplete&quot;) Then
            strInput = &quot;Y&quot;
            If Not isAutomated Then
                WScript.Echo  vbCRLF &amp; &quot;Would you like to reboot now to complete the installation? (Y/[N])&quot;
                strInput = WScript.StdIn.Readline
                WScript.Echo 
            End If
            If (strInput = &quot;Y&quot; or strInput = &quot;y&quot;) Then
                WScript.Echo &quot;Committing installation...&quot; &amp; vbCRLF
                installer.Commit(0)
                WScript.Echo &quot;Triggering restart in 30 seconds...&quot; &amp; vbCRLF
                Dim oShell
                Set oShell = WScript.CreateObject(&quot;WScript.Shell&quot;)
                oShell.Run &quot;shutdown /r /t 30&quot;, 1
            End If     
        End If
    End If
End If

WScript.Quit (returnValue)
</code></pre>
<p>Â </p>
<p>Â </p>
</body>
