<!DOCTYPE html>
<html lang="en">
<head>
<meta charset="utf-8">
<title>Exporting a snapshot of a virtual machine</title>
<style>
table th { border: 1px solid; }
table td { border: 1px solid; }
</style>
</head>
<body>
<hr />
<h2>description: The following C# and Visual Basic Scripting Edition (VBScript) samples demonstrate exporting a snapshot of a virtual machine.
ms.assetid: 4DEC4962-99E1-42BB-81B1-8530BF9C4B92
title: Exporting a snapshot of a virtual machine
ms.topic: article
ms.date: 05/31/2018</h2>
<h1>Exporting a snapshot of a virtual machine</h1>
<p>The following C# and Visual Basic Scripting Edition (VBScript) samples demonstrate exporting a snapshot of a virtual machine. Samples that demonstrate exporting a virtual machine can be found on the <a href="exportsystemdefinition-msvm-virtualsystemmanagementservice.html"><strong>ExportSystemDefinition</strong></a> topic.</p>
<p>The referenced C# utilities can be found in <a href="common-utilities-for-the-virtualization-samples-v2.html">Common utilities for the virtualization samples (V2)</a>.</p>
<pre lang="CSharp"><code>using System;
using System.IO;
using System.Management;

namespace HyperVSamples
{
    class ExportSystemDefinitionSnapshotsClass
    {

        static ManagementObject GetLastVirtualSystemSnapshot(ManagementObject vm)
        {
            ManagementObjectCollection settings = vm.GetRelated(
                &quot;Msvm_VirtualSystemSettingData&quot;,
                &quot;Msvm_MostCurrentSnapshotInBranch&quot;,
                null,
                null,
                &quot;Dependent&quot;,
                &quot;Antecedent&quot;,
                false,
                null);


            ManagementObject virtualSystemsetting = null;
            foreach (ManagementObject setting in settings)
            {
                Console.WriteLine(setting.Path.Path);
                Console.WriteLine(setting[&quot;ElementName&quot;]);
                virtualSystemsetting = setting;
            }
            return virtualSystemsetting;
        }


        static string GetVirtualSystemExportSettingDataInstance(ManagementScope scope, ManagementObject snapshotSettingData)
        {
            ManagementPath settingPath = new ManagementPath(&quot;Msvm_VirtualSystemExportSettingData&quot;);

            ManagementClass exportSettingDataClass = new ManagementClass(scope, settingPath, null);
            ManagementObject exportSettingData = exportSettingDataClass.CreateInstance();

            exportSettingData[&quot;CopySnapshotConfiguration&quot;] = 2;
            exportSettingData[&quot;CreateVmExportSubdirectory&quot;] = true;
            exportSettingData[&quot;SnapshotVirtualSystem&quot;] = snapshotSettingData.Path.Path;

            string settingData = exportSettingData.GetText(TextFormat.CimDtd20);

            exportSettingData.Dispose();
            exportSettingDataClass.Dispose();

            return settingData;
        }


        static void ExportSystemDefinitionSnapshots(string vmName, string exportDirectory)
        {
            ManagementScope scope = new ManagementScope(@&quot;root\virtualization\v2&quot;, null);
            ManagementObject virtualSystemService = Utility.GetServiceObject(scope, &quot;Msvm_VirtualSystemManagementService&quot;);

            ManagementBaseObject inParams = virtualSystemService.GetMethodParameters(&quot;ExportSystemDefinition&quot;);

            ManagementObject vm = Utility.GetTargetComputer(vmName, scope);
            ManagementObject snapshotSettingData = GetLastVirtualSystemSnapshot(vm);

            if ( snapshotSettingData == null )
            {
                Console.WriteLine(&quot;There are not snapshots in the VM to be exported...&quot;);
                return;
            }

            inParams[&quot;ComputerSystem&quot;] = vm.Path.Path;
            
            if (!Directory.Exists(exportDirectory))
            {
                Directory.CreateDirectory(exportDirectory);
            }
            inParams[&quot;ExportDirectory&quot;] = exportDirectory;
            inParams[&quot;ExportSettingData&quot;] = GetVirtualSystemExportSettingDataInstance(scope, snapshotSettingData);

            ManagementBaseObject outParams = virtualSystemService.InvokeMethod(&quot;ExportSystemDefinition&quot;, inParams, null);

            if ((UInt32)outParams[&quot;ReturnValue&quot;] == ReturnCode.Started)
            {
                if (Utility.JobCompleted(outParams, scope))
                {
                    Console.WriteLine(&quot;VM '{0}' were exported successfully.&quot;, vm[&quot;ElementName&quot;]);
                }
                else
                {
                    Console.WriteLine(&quot;Failed to export VM&quot;);
                }
            }
            else if ((UInt32)outParams[&quot;ReturnValue&quot;] == ReturnCode.Completed)
            {
                Console.WriteLine(&quot;VM '{0}' were exported successfully.&quot;, vm[&quot;ElementName&quot;]);
            }
            else
            {
                Console.WriteLine(&quot;Export virtual system failed with error:{0}&quot;, outParams[&quot;ReturnValue&quot;]);
            }

            inParams.Dispose();
            outParams.Dispose();
            vm.Dispose();
            virtualSystemService.Dispose();
        }

        static void Main(string[] args)
        {
            if (args != null &amp;&amp; args.Length != 2)
            {
                Console.WriteLine(&quot;Usage: ExportSystemDefinitionSnapshots vmName exportDirectory&quot;);
                return;
            }
            ExportSystemDefinitionSnapshots(args[0], args[1]);
        }
    }
}
</code></pre>
<pre lang="VB"><code>
option explicit 

dim objWMIService
dim managementService
dim fileSystem

const JobStarting = 3
const JobRunning = 4
const JobCompleted = 7
const wmiStarted = 4096
const wmiSuccessful = 0

Main()


'-----------------------------------------------------------------
' Main
'-----------------------------------------------------------------
Sub Main()

    dim computer, objArgs, vmName, vm, exportDirectory
    
    set fileSystem = Wscript.CreateObject(&quot;Scripting.FileSystemObject&quot;)
    computer = &quot;.&quot;
    set objWMIService = GetObject(&quot;winmgmts:\\&quot; &amp; computer &amp; &quot;\root\virtualization\v2&quot;)
    set managementService = objWMIService.ExecQuery(&quot;select * from Msvm_VirtualSystemManagementService&quot;).ItemIndex(0)
    
    set objArgs = WScript.Arguments
    if WScript.Arguments.Count = 2 then
        vmName = objArgs.Unnamed.Item(0)
        exportDirectory = objArgs.Unnamed.Item(1)
    else
        WScript.Echo &quot;usage: cscript ExportSystemDefinitionSnapshots.vbs vmName exportDirectoryName&quot;
        WScript.Quit(1)
    end if
    
    set vm = GetComputerSystem(vmName)

    if ExportSystemDefinitionSnapshots(vm, exportDirectory) then
        WriteLog &quot;Done&quot;
        WScript.Quit(0)
    else
        WriteLog &quot;ExportSystemDefinitionSnapshots Failed.&quot;
        WScript.Quit(1)
    end if
End Sub

'-----------------------------------------------------------------
' Retrieve Msvm_VirtualComputerSystem from base on its ElementName
'-----------------------------------------------------------------
Function GetComputerSystem(vmElementName)
    On Error Resume Next
    dim query
    query = Format1(&quot;select * from Msvm_ComputerSystem where ElementName = '{0}'&quot;, vmElementName)
    set GetComputerSystem = objWMIService.ExecQuery(query).ItemIndex(0)
    if (Err.Number &lt;&gt; 0) then
        WriteLog Format1(&quot;Err.Number: {0}&quot;, Err.Number)
        WriteLog Format1(&quot;Err.Description:{0}&quot;,Err.Description)
        WScript.Quit(1)
    end if
End Function

'-----------------------------------------------------------------
' Get Last Snapshot of a Virtual System
'-----------------------------------------------------------------
Function GetLastVirtualSystemSnapshot(computerSystem, snapshot)

    dim objVSSDs, vssd
    dim query 

    GetLastVirtualSystemSnapshot = false

    query = Format1(&quot;ASSOCIATORS OF {{0}} WHERE resultClass = Msvm_VirtualSystemsettingData&quot;, computerSystem.Path_.Path)
    set objVSSDs = objWMIService.ExecQuery(query)
    
    for each vssd in objVSSDs
    if vssd.SettingType = 5 then 'SettingType = 5 means it is a snapshot
            GetLastVirtualSystemSnapshot = true
        set snapshot = vssd
    end if
    next
End Function

'-----------------------------------------------------------------
' Export a virtual system
'-----------------------------------------------------------------
Function ExportSystemDefinitionSnapshots(computerSystem, exportDirectory)

    dim objInParam, objOutParams 
    dim query
    dim computer
    dim exportSettingData
    dim lastSnapshot
    dim isSnapshotPresent

    ExportSystemDefinitionSnapshots = false
    
    if Not fileSystem.FolderExists(exportDirectory) then
        fileSystem.CreateFolder(exportDirectory)
    end if
    
    set objInParam = managementService.Methods_(&quot;ExportSystemDefinition&quot;).InParameters.SpawnInstance_()
    objInParam.ComputerSystem = computerSystem.Path_.Path

    query = Format1(&quot;ASSOCIATORS OF {{0}} WHERE resultClass = Msvm_VirtualSystemExportSettingData&quot;, computerSystem.Path_.Path)
    set exportSettingData = objWMIService.ExecQuery(query).ItemIndex(0)
    exportSettingData.CopyVmStorage = true
    exportSettingData.CopyVmRuntimeInformation = true
    exportSettingData.CreateVmExportSubdirectory = true
    exportSettingData.CopySnapshotConfiguration = 2
    isSnapshotPresent = GetLastVirtualSystemSnapshot(computerSystem, lastSnapshot)
    if isSnapshotPresent = false then
        WriteLog &quot;No snapshots were found on the VM when trying to export snapshot as VM.&quot;
        ExportSystemDefinitionSnapshots = false
    Exit Function
    end if

    exportSettingData.SnapshotVirtualSystem = lastSnapshot.Path_.Path
    objInParam.ExportSettingData = exportSettingData.GetText_(1)

    objInParam.ExportDirectory = exportDirectory
    
    set objOutParams = managementService.ExecMethod_(&quot;ExportSystemDefinition&quot;, objInParam)

    if objOutParams.ReturnValue = wmiStarted then
        if (WMIJobCompleted(objOutParams)) then
            ExportSystemDefinitionSnapshots = true
        end if
    elseif (objOutParams.ReturnValue = wmiSuccessful) then
        ExportSystemDefinitionSnapshots = true
    else
        WriteLog Format1(&quot;ExportSystemDefinitionSnapshots failed with ReturnValue {0}&quot;, objOutParams.ReturnValue)
    end if

End Function

'-----------------------------------------------------------------
' Handle wmi Job object
'-----------------------------------------------------------------
Function WMIJobCompleted(outParam)
    
    dim WMIJob, jobState

    set WMIJob = objWMIService.Get(outParam.Job)

    WMIJobCompleted = true

    jobState = WMIJob.JobState

    while jobState = JobRunning or jobState = JobStarting
        WriteLog Format1(&quot;In progress... {0}% completed.&quot;,WMIJob.PercentComplete)
        WScript.Sleep(1000)
        set WMIJob = objWMIService.Get(outParam.Job)
        jobState = WMIJob.JobState
    wend

    if (jobState &lt;&gt; JobCompleted) then
        WriteLog Format1(&quot;ErrorCode:{0}&quot;, WMIJob.ErrorCode)
        WriteLog Format1(&quot;ErrorDescription:{0}&quot;, WMIJob.ErrorDescription)
        WMIJobCompleted = false
    end if

End Function

'-----------------------------------------------------------------
' Create the console log files.
'-----------------------------------------------------------------
Sub WriteLog(line)
    dim fileStream
    set fileStream = fileSystem.OpenTextFile(&quot;.\ExportSystemDefinition-ExportSnapshot.log&quot;, 8, true)
    WScript.Echo line
    fileStream.WriteLine line
    fileStream.Close

End Sub

'------------------------------------------------------------------------------
' The string formatting functions to avoid string concatenation.
'------------------------------------------------------------------------------
Function Format2(myString, arg0, arg1)
    Format2 = Format1(myString, arg0)
    Format2 = Replace(Format2, &quot;{1}&quot;, arg1)
End Function

'------------------------------------------------------------------------------
' The string formatting functions to avoid string concatenation.
'------------------------------------------------------------------------------
Function Format1(myString, arg0)
    Format1 = Replace(myString, &quot;{0}&quot;, arg0)
End Function
</code></pre>
<h2>Related topics</h2>
<p><a href="exporting-the-configuration-of-a-virtual-machine.html">Exporting the configuration of a virtual machine</a></p>
<p><a href="exportsystemdefinition-msvm-virtualsystemmanagementservice.html"><strong>ExportSystemDefinition</strong></a></p>
</body>
